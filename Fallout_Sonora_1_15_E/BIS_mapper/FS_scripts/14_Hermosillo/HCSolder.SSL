#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_HCSolder
#define TOWN_REP_VAR                    GVAR_TOWN_REP_HERMOSILLO  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_HERMO_FORT       // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_HERMO_SOLDIER      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      70 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       if (item_amnt(self_obj,PID_TEST) > 0) then begin
          if (cur_map_index == MAP_HERMO_CASA) then begin
           if (item_amnt(self_obj,PID_TEST) == 3) then begin
           set_local_var(LVAR_Tile_B,19899); // тюремщик
           end
           if (item_amnt(self_obj,PID_TEST) == 2) then begin
           set_local_var(LVAR_Tile_B,28501); // южный патруль
           end
           if (item_amnt(self_obj,PID_TEST) == 1) then begin
           set_local_var(LVAR_Tile_B,12104); // северный патруль
           end
          end
         item_remove(self_obj,PID_TEST,999)
       end
       // added to allow for combat taunts to be either pure spanish, or 'translated' via the Espanol Trait - cambragol
       if DudeTraitHas(TRAIT_espanol) then begin
          critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_HERMO_SOLDIER_ENG);
       end

       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (local_var(LVAR_Tile_B) > 0) then begin
       timer_move_patrol(HomeTile,local_var(LVAR_Tile_B))
       end
       else if ((map_var(MVAR_HERMO_ALARM_A) > 0) and (cur_map_index == MAP_HERMO_CASA)) then begin
       timer_move_random_tile(20304,8)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   variable attakParam;
   attakParam := global_var(GVAR_HERMO_DUDE_ALARM);

   if ((armor_pid_dude_var == PID_ARMOR_COMBAT_MEX) and (tile_distance(dude_tile, self_tile) > 1)) then begin
   attakParam := 0;
   end

   set_critter_options(attakParam,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
   set_global_var(GVAR_HERMO_DUDE_ALARM,2);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (armor_pid_dude_var == PID_ARMOR_COMBAT_MEX) then begin
      // added to make proper use of espanol trait - cambragol
      if DudeTraitHas(TRAIT_espanol) then begin
      floater(self_obj,105,COLOR_MSG_RED);
      end
      else begin
      floater(self_obj,104,COLOR_MSG_RED);
      end
   end
   else begin
      // added to make proper use of espanol trait - cambragol
      if DudeTraitHas(TRAIT_espanol) then begin
      floater(self_obj,103,COLOR_MSG_RED);
      end
      else begin
      floater(self_obj,102,COLOR_MSG_RED);
      end
   end
end
