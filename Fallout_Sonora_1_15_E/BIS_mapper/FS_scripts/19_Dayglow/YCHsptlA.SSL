#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Госпиталь - черный старатель

#define NAME                            SCRIPT_YCHsptlA
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_BANDITS          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_BANDIT             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_BAD          // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      40 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

variable AttakParam;

import variable obj_critter_a; // указатель на гуля
import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       if (map_first_run == true) then begin
       inc_map_var(MVAR_DIEGOHSPTL_BANDLIFE);
       end
       set_start_home

       set_obj_visibility(self_obj, 0);
       if (map_var(MVAR_DIEGOHSPTL_TIMER) > 0) then begin
          if (map_var(MVAR_DIEGOHSPTL_TIMER) < game_time) then begin
          set_obj_visibility(self_obj, 1);
          destroy_object(self_obj);
          end
       end

       if (item_amnt(self_obj,PID_TEST) > 0) then begin
           if (cur_map_index == MAP_DAYGLOW_NATIONALCITY) then begin
              if (item_amnt(self_obj,PID_TEST) == 1) then begin
              set_local_var(LVAR_Tile_B,13468); //
              end
              if (item_amnt(self_obj,PID_TEST) == 2) then begin
              set_local_var(LVAR_Tile_B,17275); //
              end
              if (item_amnt(self_obj,PID_TEST) == 3) then begin
              set_local_var(LVAR_Tile_B,-1); // лидер (стоит на месте)
              end
           end
           item_remove(self_obj,PID_TEST,999)
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (local_var(LVAR_Tile_B) > 0) then begin
       timer_move_patrol(HomeTile,local_var(LVAR_Tile_B))
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       if (local_var(LVAR_Tile_B) == -1) then begin
          if ((global_var(GVAR_DAYGLOW_STATUS_ZERAF) == 0) and (random(0,1) == 0)) then begin
          //timer_float_reply(130, 136)
          timer_floater(130, 136,COLOR_MSG_NORMAL,random(70,200))
          end
          else if (global_var(GVAR_DAYGLOW_STATUS_ZERAF) == 2) then begin
          //timer_float_reply(140, 148)
          timer_floater(140, 149,COLOR_MSG_RED,random(70,200))
          end
          else begin
          //timer_float_reply(120, 127)
          timer_floater(120, 127,COLOR_MSG_NORMAL,random(70,200))
          end
       end
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   AttakParam := 2; // ! изначально предполагалось, что со старателями можно договориться, но ради ускорения проекта придется сократить эту опцию
   if (partyPidMtn!=0) then begin
   AttakParam := 2;
   end
   if (partyPidMr1!=0) then begin
   AttakParam := 2;
   end
   if (partyPidMr2!=0) then begin
   AttakParam := 2;
   end
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   AttakParam := 2;
   end
   set_critter_options(AttakParam,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   if (self_visible == true) then begin
       if (global_var(GVAR_DAYGLOW_STATUS_ZERAF) == 1) then begin
          if (obj_critter_a > 0) then begin
          if (obj_can_see_obj(self_obj,obj_critter_a) == true) then begin
               if dude_is_sneaking then begin
                  if (tile_distance_objs(self_obj,obj_critter_a) <= 3) then begin
                  attack(obj_critter_a);
                  end
               end
               else begin
                  attack(obj_critter_a);
               end
          end
          end
       end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      if (dude_elevation == self_elevation) then begin
          if (random(0,1) == 1) then begin
          floater(self_obj, random(110,113), COLOR_MSG_RED);
          end
      end
   end
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   dec_map_var(MVAR_DIEGOHSPTL_BANDLIFE);
   if (map_var(MVAR_DIEGOHSPTL_BANDLIFE) <= 0) then begin
   set_map_var(MVAR_DIEGOHSPTL_TIMER, ONE_GAME_HOUR + game_time);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_DAYGLOW_STATUS_ZERAF) >= 3) then begin
      floater(self_obj, 104, COLOR_MSG_RED);
   end
   else if DudePerkHas(PERK_ghouling_fiziology) then begin
      floater(self_obj, 102, COLOR_MSG_RED);
   end
   else begin
      floater(self_obj, 103, COLOR_MSG_RED);
   end
end
