#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Супермутанты

#define NAME                            SCRIPT_YCIsdMtB
#define TOWN_REP_VAR                    -1  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_MOTEL_MUTANT     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SUPERMUTANT        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog;
procedure NodeDialog2;
procedure NodeDialog3;
procedure NodeDialog4;
procedure NodeDialog5;
procedure NodeDialog6;
procedure NodeDialog7;

//---------------------

//#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

variable attakParam;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);
       if (getQuest(GVAR_DAYGLOW_MUTGOPSTROP) == qStart) then begin
       set_obj_visibility(self_obj, 0);
       end

       if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) < 0) then begin
       set_obj_visibility(self_obj, 1);
       destroy_object(self_obj);
       end
       else if (getQuest(GVAR_DAYGLOW_MUTGOPSTROP) >= qEnd) then begin
       destroy_object(self_obj);
       end
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) < 0) then begin
         set_local_var(LVAR_Home_Tile,22263);
       end
       timer_move_to_home
       if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) < 0) then begin
         if (self_tile == HomeTile) then begin
         set_obj_visibility(self_obj, 1);
         end
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   attakParam := 0;
   if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) > 0) then begin
   attakParam := 2;
   end
   set_critter_options(attakParam,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (attakParam==0) then begin
      if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) == 0) then begin
      dialogue_system_forcing(6)
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,3,3,3) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   GomerAttakDude
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   inc_map_var(MVAR_DIEGOPTRL_MUTNTLIFE);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) < 0) then begin
      floater(self_obj, 102, COLOR_MSG_NORMAL);
   end
   else if (map_var(MVAR_DIEGOPTRL_MUTNTLIFE) > 0) then begin
      floater(self_obj, 103, COLOR_MSG_RED);
   end
   else begin
      setReaction(-1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
      fracReaction(GVAR_TITUL_REBIRTH,20) // Поправка к отношению за принадлежность к фракциям
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   GomerAttakDude
   setNodeCombat;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   call NodeDialog;
end

procedure NodeDialog begin
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   Reply(212)
   BAnswer(213,1,Node998)
   GAnswerSkill(215,1,NodeDialog6,SKILL_CONVERSANT,20,214,NodeDialog5)
   end
   else if (partyPidMtn != 0) then begin
   Reply(220)
   NAnswer(221,1,NodeDialog7)
   end
   else begin
   Reply(200)
   BAnswer(201,1,Node998)
   GAnswerSkill(206,1,NodeDialog3,SKILL_CONVERSANT,67,202,NodeDialog2)
   end
end

procedure NodeDialog2 begin
   Reply(203)
   BAnswer(204,1,Node998)
   NAnswer(205,1,Node998)
end

procedure NodeDialog3 begin
   zReply(207)
   BAnswer(209,1,Node998)
   GAnswer(208,1,NodeDialog4)
end

procedure NodeDialog4 begin
   set_map_var(MVAR_DIEGOPTRL_MUTNTLIFE,-100);
   exp_add(100);
   Reply(210)
   NAnswer(211,1,Node999)
end

procedure NodeDialog5 begin
   Reply(216)
   zNAnswer(217,1,Node998)
end

procedure NodeDialog6 begin
   set_map_var(MVAR_DIEGOPTRL_MUTNTLIFE,-100);
   exp_add(100);
   Reply(218)
   NAnswer(219,1,Node999)
end

procedure NodeDialog7 begin
   set_map_var(MVAR_DIEGOPTRL_MUTNTLIFE,-100);
   exp_add(100);
   Reply(222)
   NAnswer(223,1,Node999)
end

