#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCCrvBos
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CARAVAN_DRIVER     // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   (REP_KILL_GOOD*2)     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      100 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure setBarterA;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeBarter;
procedure NodeBarter2;
procedure NodeBarter3;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;
procedure NodeAnswer106;
procedure NodeAnswer107;
procedure NodeAnswer108;
procedure NodeQstItems;
procedure NodeQstItems2;
procedure NodeQstItems3;
procedure NodeQstItems4;
procedure NodeQstItems4_1;
procedure NodeQstItems5;
procedure NodeQstItems5_1;
procedure NodeQstItems6;
procedure NodeQstItems6_1;
procedure NodeQstItems7;
procedure NodeQstItems7_1;
procedure NodeQstItems8;
procedure NodeQstItems8_1;
procedure NodeQstItems9;

//---------------------

//#define LVAR_Flag_A                     (8) // отвечает за ...

//---------------------



procedure start begin
   //set_global_var(GVAR_DAYGLOW_STATUS_RUTGER, 1); -- чит
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);
   end
end

procedure map_update_p_proc begin
   if (global_var(GVAR_DAYGLOW_STATUS_RUTGER) > 0) then begin
       set_obj_visibility(self_obj, 0);
   end
end

procedure setBarterA begin
        check_restock_item(PID_M_FRUIT, 7, 7, 100)
        check_restock_item(PID_W_GRENADE_PULSE, 7, 7, 100)
        check_restock_item(PID_AMMO_50_AE, 40, 40, 100)
        check_restock_item(PID_W_PISTOL_50, 1, 1, 100)
        check_restock_item(PID_ARMOR_COMBAT, 1, 1, 100)

        //- крышки
        check_restock_many(1000,1100)
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_DAYGLOW_STATUS_RUTGER, -1);
   if (self_elevation == 0) then begin
   set_global_var(GVAR_DAYGLOW_STATUS_RUTGER, -2);
   end
   if (getQuest(GVAR_DAYGLOW_QST_CARAVAN) < qEnd) then begin
   setQuest(GVAR_DAYGLOW_QST_CARAVAN, qStop);
   end
   if (map_var(MVAR_DIEGO_C_CARREGULATOR) != 0) then begin
   set_map_var(MVAR_DIEGO_C_CARREGULATOR, 0);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(0) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   item_add(dude_obj,PID_W_RIFLE_ASSAULT,1)
   if (getQuest(GVAR_DAYGLOW_MAIN_C) == qStart) then begin
   setQuest(GVAR_DAYGLOW_MAIN_C, qEnd);
   end
   exp_add(1500);
   zReply(200)
   NAnswer(201,1,NodeSndDialog)
   if (bitDialog(bit_2) == 0) then begin
   NAnswer(202,1,NodeBarter)
   end
end

procedure NodeBarter begin
   bitDialogExp(bit_2,0)
   call setBarterA;
   Reply(203)
   NAnswer(204,1,NodeBarter2)
end

procedure NodeBarter2 begin
   goBarterMod;
   Reply(203)
   NAnswer(237,1,NodeBarter3)
end

procedure NodeBarter3 begin
   if (getQuest(GVAR_DAYGLOW_QST_CARAVAN) == qZero) then begin
   Reply(205)
   NAnswer(206,1,NodeQstItems)
   NAnswer(207,1,Node999)
   end
   else begin
   Reply(208)
   NAnswer(210,1,NodeSndDialog)
   NAnswer(209,1,Node999)
   end
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (getQuest(GVAR_DAYGLOW_MAIN_D) == 102) then begin
   Reply(266)
   end
   else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 101) then begin
   Reply(239)
   end
   else if (partyPidMtn != 0) then begin
   Reply(238)
   end
   else begin
   Reply(235)
   end

   if (bitDialog(bit_2) == 0) then begin
   NAnswer(202,1,NodeBarter)
   end else begin
   NAnswer(211,1,NodeBarter2)
   end

   if (getQuest(GVAR_DAYGLOW_QST_CARAVAN) == qStart) then begin
   NAnswer(214,1,NodeQstItems2)
   end

   NAnswer(240,1,NodeAnswer100)
   NAnswer(249,1,NodeAnswer103)
   NAnswer(254,1,NodeAnswer105)

   NAnswer(236,1,Node999)
end

//--- РАССПРОСЫ

procedure NodeAnswer100 begin
   Reply(241)
   NAnswer(242,1,NodeAnswer101)
   NAnswer(248,1,NodeAnswer103)
   NAnswer(244,1,NodeSndDialog)
end

procedure NodeAnswer101 begin
   Reply(243)
   if (town_known(AREA_PHOENIX) == MARK_STATE_VISITED) then begin
   NAnswer(245,5,NodeAnswer102)
   end
   NAnswer(262,1,NodeAnswer103)
   NAnswer(244,1,NodeSndDialog)
end

procedure NodeAnswer102 begin
   bitDialogExp(bit_3,100)
   Reply(246)
   NAnswer(247,1,NodeAnswer100)
end

procedure NodeAnswer103 begin
   Reply(250)
   NAnswer(251,1,NodeAnswer101)
   NAnswer(252,5,NodeAnswer104)
   NAnswer(244,1,NodeSndDialog)
end

procedure NodeAnswer104 begin
   bitDialogExp(bit_4,100)
   Reply(253)
   NAnswer(261,1,NodeAnswer105)
   NAnswer(251,1,NodeAnswer101)
   NAnswer(264,5,NodeAnswer108)
   NAnswer(244,1,NodeSndDialog)
end

procedure NodeAnswer105 begin
   Reply(255)
   NAnswer(256,1,NodeAnswer106)
   NAnswer(257,1,NodeSndDialog)
end

procedure NodeAnswer106 begin
   Reply(258)
   NAnswer(256,1,NodeAnswer107)
   NAnswer(257,1,NodeSndDialog)
end

procedure NodeAnswer107 begin
   bitDialogExp(bit_5,50)
   Reply(263)
   if (town_known(AREA_INFIERNO) == MARK_STATE_VISITED) then begin
   NAnswer(260,1,NodeSndDialog)
   end
   else begin
   zNAnswer(259,1,NodeSndDialog)
   end
end

procedure NodeAnswer108 begin
   bitDialogExp(bit_6,50)
   Reply(265)
   NAnswer(244,1,NodeSndDialog)
end

//--- КВЕСТ НА ПОИСК АРТЕФАКТОВ

procedure NodeQstItems begin
   setQuest(GVAR_DAYGLOW_QST_CARAVAN, qStart);
   Reply(212)
   NAnswer(213,1,Node999)
end

procedure NodeQstItems2 begin
   Reply(215)
   if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_6) == 0) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_1)>0) then begin
      NAnswer(mstr(230)+proto_data(PID_Q_DAYGLOW_CARAVAN_1, it_name)+mstr(231),1,NodeQstItems4_1)
      end
      else begin
      NAnswer(220,1,NodeQstItems4)
      end
   end
   if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_7) == 0) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_2)>0) then begin
      NAnswer(mstr(230)+proto_data(PID_Q_DAYGLOW_CARAVAN_2, it_name)+mstr(231),1,NodeQstItems5_1)
      end
      else begin
      NAnswer(222,1,NodeQstItems5)
      end
   end
   if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_8) == 0) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_3)>0) then begin
      NAnswer(mstr(230)+proto_data(PID_Q_DAYGLOW_CARAVAN_3, it_name)+mstr(231),1,NodeQstItems6_1)
      end
      else begin
      NAnswer(224,1,NodeQstItems6)
      end
   end
   if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_9) == 0) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_4)>0) then begin
      NAnswer(mstr(230)+proto_data(PID_Q_DAYGLOW_CARAVAN_4, it_name)+mstr(231),1,NodeQstItems7_1)
      end
      else begin
      NAnswer(226,1,NodeQstItems7)
      end
   end
   if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_10) == 0) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_5)>0) then begin
      NAnswer(mstr(230)+proto_data(PID_Q_DAYGLOW_CARAVAN_5, it_name)+mstr(231),1,NodeQstItems8_1)
      end
      else begin
      NAnswer(228,1,NodeQstItems8)
      end
   end
   NAnswer(217,1,NodeQstItems3)
   NAnswer(216,1,Node999)
end

procedure NodeQstItems3 begin
   Reply(218)
   NAnswer(219,1,NodeQstItems2)
end

procedure NodeQstItems4 begin
   Reply(221)
   NAnswer(219,1,NodeQstItems2)
end

procedure NodeQstItems4_1 begin
   if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_1) <= 0) then begin
      call NodeQstItems2;
   end
   else begin
      set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_6);
      item_remove(dude_obj,PID_Q_DAYGLOW_CARAVAN_1,1)
      call NodeQstItems9;
   end
end

procedure NodeQstItems5 begin
   Reply(223)
   NAnswer(219,1,NodeQstItems2)
end

procedure NodeQstItems5_1 begin
   if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_2) <= 0) then begin
      call NodeQstItems2;
   end
   else begin
      set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_7);
      item_remove(dude_obj,PID_Q_DAYGLOW_CARAVAN_2,1)
      call NodeQstItems9;
   end
end

procedure NodeQstItems6 begin
   Reply(225)
   NAnswer(219,1,NodeQstItems2)
end

procedure NodeQstItems6_1 begin
   if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_3) <= 0) then begin
      call NodeQstItems2;
   end
   else begin
      set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_8);
      item_remove(dude_obj,PID_Q_DAYGLOW_CARAVAN_3,1)
      call NodeQstItems9;
   end
end

procedure NodeQstItems7 begin
   if (map_var(MVAR_DIEGO_C_CARREGULATOR) == 0) then begin
   set_map_var(MVAR_DIEGO_C_CARREGULATOR, 1);
   end
   Reply(227)
   NAnswer(219,1,NodeQstItems2)
end

procedure NodeQstItems7_1 begin
   if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_4) <= 0) then begin
      call NodeQstItems2;
   end
   else begin
      set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_9);
      set_map_var(MVAR_DIEGO_C_CARREGULATOR, 4);
      set_map_var(MVAR_DIEGO_C_ROBOT_TIMER, ONE_GAME_HOUR + game_time);
      item_remove(dude_obj,PID_Q_DAYGLOW_CARAVAN_4,1)
      call NodeQstItems9;
   end
end

procedure NodeQstItems8 begin
   Reply(229)
   NAnswer(219,1,NodeQstItems2)
end

procedure NodeQstItems8_1 begin
   if (item_amnt(dude_obj,PID_Q_DAYGLOW_CARAVAN_5) <= 0) then begin
      call NodeQstItems2;
   end
   else begin
      set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_10);
      item_remove(dude_obj,PID_Q_DAYGLOW_CARAVAN_5,1)
      call NodeQstItems9;
   end
end

procedure NodeQstItems9 begin
   item_add(dude_obj,PID_BOOTLE_CAPS,400)
   exp_add(700);

   if (getQuest(GVAR_DAYGLOW_QST_CARAVAN) < qEnd) then begin
      if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_6) != 0) then begin
         if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_7) != 0) then begin
            if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_8) != 0) then begin
               if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_9) != 0) then begin
                  if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_10) != 0) then begin
                  display_mstr(234);
                  setQuest(GVAR_DAYGLOW_QST_CARAVAN, qEnd);
                  end
               end
            end
         end
      end
   end

   Reply(232)
   NAnswer(233,1,Node999)
end
