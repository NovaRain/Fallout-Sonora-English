#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCCrvGrd
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CARAVAN_DRIVER     // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_PartyFlag                     (6) // следует за ГГ
#define LVAR_PartyMsgSneak                 (7) // фраза при использовании скрытности
#define LVAR_TileBarCombat                 (8) // номер тайла для размещения в баре на время драки

import variable obj_critter_a; // указатель на себя во время побега

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   set_local_var(LVAR_PartyMsgSneak, 0);
   if (is_loading_game == false) then begin
       set_start_home

       if self_is_female then begin
       set_local_var(LVAR_TileBarCombat,16915);
       end
       else begin
       set_local_var(LVAR_TileBarCombat,16917);
       end
       if ((map_var(MVAR_DIEGO_C_BAR_COMBAT) == 1) and (local_var(LVAR_TileBarCombat) > 0)) then begin
       move_to(self_obj, local_var(LVAR_TileBarCombat), 0);
       end

       set_local_var(LVAR_PartyFlag, 0);
       set_obj_visibility(self_obj, 1);
   end
end

procedure map_update_p_proc begin
   if (self_elevation == 0) then begin

       if (local_var(LVAR_PartyFlag)>0) then begin
          obj_critter_a := 0;
          if self_is_female then begin
          set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_2);
          set_local_var(LVAR_Home_Tile, tile_num_in_direction(24328, random(0,2), random(2,5)));
          end
          else begin
          set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_1);
          set_local_var(LVAR_Home_Tile, tile_num_in_direction(24328, random(3,5), random(2,5)));
          end
          set_local_var(LVAR_PartyFlag, 0);
          set_local_var(LVAR_Home_Rotat, random(0,5));
          display_mstr(106);
          floater(self_obj, 103, COLOR_MSG_NORMAL);
          add_timer_event(self_obj, 30, 1);
       end

       if (global_var(GVAR_DAYGLOW_STATUS_RUTGER) >= 0) then begin
          set_obj_visibility(self_obj, 0);
       end
   end
   else begin
       if (global_var(GVAR_DAYGLOW_STATUS_RUTGER) == 0) then begin
          if (getQuest(GVAR_DAYGLOW_MAIN_C) == qStart) then begin
          set_obj_visibility(self_obj, 0);
          end
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (local_var(LVAR_PartyFlag)>0) then begin
       end
       else if ((map_var(MVAR_DIEGO_C_BAR_COMBAT) == 1) and (local_var(LVAR_TileBarCombat) > 0)) then begin
       timer_move_to_tile(1,local_var(LVAR_TileBarCombat),2)
       end
       else if (self_elevation == 0) then begin
       timer_move_random_home(5)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   if (map_var(MVAR_DIEGO_C_MUTANT_PARTY) == 1) then begin
      if (local_var(LVAR_PartyFlag)>0) then begin
         obj_critter_a := self_obj;
         if dude_is_sneaking then begin
            if (local_var(LVAR_PartyMsgSneak) == 0) then begin
            floater(self_obj, 105, COLOR_MSG_LIGHT_GREY);
            set_local_var(LVAR_PartyMsgSneak,1);
            end
         end
         partyRunToDude
      end
   end

   if (map_var(MVAR_DIEGO_C_BAR_COMBAT) == 1) then begin
      obj_critter_a := self_obj;
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   obj_critter_a := 0;
   if self_is_female then begin
   set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_13);
   set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_2);
   end
   else begin
   set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_12);
   set_gvar_bit_on(GVAR_DAYGLOW_STATUS_CARAVAN, bit_1);
   end
   if (map_var(MVAR_DIEGO_C_BAR_COMBAT) == 1) then begin
   set_map_var(MVAR_DIEGO_C_BAR_COMBAT, 2);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (self_elevation == 0) then begin
       if (map_var(MVAR_DIEGO_C_BAR_COMBAT) == 1) then begin
       floater(self_obj, 107, COLOR_MSG_RED);
       end
       else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 100) then begin
       floater(self_obj, 130, COLOR_MSG_NORMAL);
       end
       else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 101) then begin
       floater(self_obj, 131, COLOR_MSG_NORMAL);
       end
       else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 102) then begin
       floater(self_obj, 132, COLOR_MSG_NORMAL);
       end
       else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 104) then begin
       floater(self_obj, 134, COLOR_MSG_NORMAL);
       end
       else if (global_var(GVAR_DAYGLOW_STATUS_RUTGER) < 0) then begin
       floater(self_obj, random(120,122), COLOR_MSG_NORMAL);
       end
       else begin
       floater(self_obj, random(110,114), COLOR_MSG_NORMAL);
       end
   end
   else begin
       if (local_var(LVAR_PartyFlag)>0) then begin
       floater(self_obj, 102, COLOR_MSG_LIGHT_GREY);
       end
       else if (map_var(MVAR_DIEGO_C_MUTANT_PARTY) == 1) then begin
       rm_timer_event(self_obj);
       set_local_var(LVAR_PartyFlag, 1);
       floater(self_obj, 104, COLOR_MSG_GREEN);
       end
       else begin
       floater(self_obj, 102, COLOR_MSG_LIGHT_GREY);
       end
   end
end
