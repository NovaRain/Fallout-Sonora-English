#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Командир теней

#define NAME                            SCRIPT_YCMutCap
#define TOWN_REP_VAR                    -1  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_MOTEL_MUTANT     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SUPERMUTANT        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog;
procedure NodeDialog2;
procedure NodeDialog3;
procedure NodeDialog4;
procedure NodeDialog5;
procedure NodeDialog6;

//---------------------

//#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

import variable obj_critter_a; // указатель на криттера 1

variable doorObj;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);
       if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) == 0) then begin
       if (getQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT) == qStart) then begin
          if ((cur_map_index == MAP_DAYGLOW_PATRUL) and (self_elevation == 0)) then begin
             if (tile_contains_obj_pid(23908,0,33555331) > 0) then begin
             destroy_object(tile_contains_pid_obj(23908, 0, 33555331));
             doorObj := create_object_sid(33555331, 23908, 0, SCRIPT_ZSDoor);
             add_timer_event(doorObj, 1, 1);
             obj_lock(doorObj);
             end
          end
       set_obj_visibility(self_obj, 0);
       end
       end
   end
end

procedure map_update_p_proc begin
   if (getQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT) <= -100) then begin
       set_obj_visibility(self_obj, 1);
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       //timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   if (self_visible == true) then begin
   obj_critter_a := self_obj;
   end
   if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) == 1) then begin
      partyRunToDude
      if ((cur_map_index == MAP_DAYGLOW_PATRUL) and (self_elevation !=0)) then begin
      dialogue_system_enter;
      end
   end
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,5,5,5) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
   if (source_obj == dude_obj) then begin
   GomerAttakDude
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP, -1);
   obj_critter_a := 0;
   kill_critter_opt
   if (source_obj == dude_obj) then begin
   GomerAttakDude
   end
end

procedure talk_p_proc begin
   if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) < 0) then begin
   //
   end
   else if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) >= 2) then begin
   floater(self_obj, 102, COLOR_MSG_GREEN);
   end
   else if ((global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) == 0) and (tile_distance_objs(self_obj,dude_obj)>4)) then begin
   end
   else begin
   setReaction(0) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   GomerAttakDude
   setNodeCombat;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) == 0) then begin
   call NodeDialog;
   end
   else begin
   call NodeDialog3;
   end
end

procedure NodeDialog begin
   Reply(200)
   NAnswer(201,0,NodeDialog2)
   NAnswer(202,0,Node999)
end

procedure NodeDialog2 begin
   set_global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP,1);
   rm_timer_event(self_obj);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
   call Node999;
end

procedure NodeDialog3 begin
   if (global_var(GVAR_DAYGLOW_STATUS_GOMER) == 2) then begin
   Reply(203)
   end
   else begin
   Reply(204)
   end

   if ((cur_map_index == MAP_DAYGLOW_PATRUL) and (self_elevation != 0)) then begin
   NAnswer(206,0,NodeDialog4)
   end
   else begin
   NAnswer(205,0,Node999)
   end
end

procedure NodeDialog4 begin
   Reply(207)
   NAnswer(208,0,NodeDialog5)
end

procedure NodeDialog5 begin
   Reply(209)
   NAnswer(210,0,NodeDialog6)
end

procedure NodeDialog6 begin
   set_global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP,2);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
   if (partyPidMtn != 0) then begin
   floater(partyPidMtn, 103, COLOR_MSG_GREEN);
   end
   exp_add(1000);
   setQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT,qEnd);
   set_obj_visibility(self_obj, 1);
   call Node999;
end
