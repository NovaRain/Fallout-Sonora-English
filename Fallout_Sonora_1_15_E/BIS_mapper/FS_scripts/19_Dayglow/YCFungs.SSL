#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCFungs
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW
#define BOX_TILE                        11  // номер тайла с личным товарным ящиком (-1, если не надо; не использовать значения <10; не может совпадать с чужими ящиками на одной локации)

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure setBarterA;
procedure setBarterB;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeSndDialog;
procedure NodeBarter;
procedure NodeBarter2;
procedure NodeSndDialog100;
procedure NodeSndDialog101;
procedure NodeSndDialog101_01;
procedure NodeSndDialog101_02;
procedure NodeSndDialog101_03;
procedure NodeSndDialog102;
procedure NodeSndDialog103;

//---------------------

//#define LVAR_Flag_A                     (8) // отвечает за ...

import variable box_stop_use;

variable mapIndx;
variable mapCoast;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_BarterBoxProc(ONE_GAME_DAY*3)
   end
end

procedure setBarterA begin
        check_restock_many(50,100)
        check_restock_item(PID_TOOL_BOOTS, 1, 1, 100)
        check_restock_item(PID_TOOL_SHOVEL, 1, 1, 100)
        check_restock_item(PID_Q_FUNGUS, 10, 12, 100)
end

procedure setBarterB begin
   //! пока пусто
   set_BarterDopusk;
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(4)
   end
   if (fixed_param == 2) then begin
       timer_float_reply(120,132)
   end
   if (fixed_param == 3) then begin
       if (mapIndx>0) then begin
          if (mapIndx == MAP_DAYGLOW_CAMP_SAN_ISIDRO) then begin
          set_global_var(GVAR_DAYGLOW_MONORAIL_MOVETO, MAP_DAYGLOW_CAMP_SAN_ISIDRO+1000);
          end
          dude_teleport_map(0,0,mapIndx,2);
       end
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (objUsedPid == PID_Q_DAYGLOW_BORDERBADGE_A) then begin
       script_overrides;
       floater(self_obj, 102, COLOR_MSG_RED);
   end
   else if (objUsedPid == PID_Q_DAYGLOW_BORDERBADGE_B) then begin
       script_overrides;
       floater(self_obj, 102, COLOR_MSG_RED);
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   kill_critter_box
end

procedure talk_p_proc begin
   setReaction(1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,85) // поправка к отношению за принадлежность к фракции
   dialog_proc_barter(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,0,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,0,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(204)
   BAnswer(206,0,NodeFstDialog5)
   NAnswer(205,0,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   Reply(207)
   NAnswer(208,0,NodeBarter)
   NAnswer(209,0,Node999)
end

procedure NodeFstDialog5 begin
   Reply(210)
   NAnswer(208,0,NodeBarter)
   NAnswer(209,0,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   mapIndx := 0;
   mapCoast := 0;
   if town_rep_is_liked then begin
   Reply(221)
   end
   else if town_rep_is_hated then begin
   zReply(222)
   end
   else begin
   zReply(221)
   end
   NAnswer(224,0,NodeBarter)
   NAnswer(230,0,NodeSndDialog100)
   NAnswer(223,0,Node999)
end

procedure NodeBarter begin
   goBarterMod;
   call NodeBarter2;
end

procedure NodeBarter2 begin
   goBarterMod;
   Reply(211)
   NAnswer(212,0,Node999)
end

procedure NodeSndDialog100 begin
   mapIndx := 0;
   Reply(231)
   NAnswer(233,0,NodeSndDialog101_01) // южный лагерь
   if ((getQuest(GVAR_DAYGLOW_MAIN_A) > qZero) or (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_28) != 0)) then begin
   NAnswer(238,0,NodeSndDialog101_02) // дамба
   end
   if ((global_var(GVAR_DAYGLOW_RADIO) >= 2) or (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_29) != 0)) then begin
   NAnswer(239,0,NodeSndDialog101_03) // Старый Город
   end
   NAnswer(232,0,Node999)
end

procedure NodeSndDialog101 begin
   Reply(234)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= mapCoast) then begin
   NAnswer(mstr(235)+mapCoast+mstr(236),0,NodeSndDialog102)
   end
   NAnswer(237,0,Node999)
end

procedure NodeSndDialog101_01 begin
   mapIndx := MAP_DAYGLOW_CAMP_SAN_ISIDRO;
   mapCoast := 300;
   call NodeSndDialog101;
end

procedure NodeSndDialog101_02 begin
   mapIndx := MAP_DAYGLOW_BONITA_DUM;
   mapCoast := 500;
   call NodeSndDialog101;
end

procedure NodeSndDialog101_03 begin
   mapIndx := MAP_DAYGLOW_CAMP_OLD_TOWN;
   mapCoast := 1500;
   call NodeSndDialog101;
end

procedure NodeSndDialog102 begin
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) < mapCoast) then begin
   call NodeSndDialog101;
   end
   else begin
   call NodeSndDialog103;
   end
end

procedure NodeSndDialog103 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,mapCoast)
   add_timer_event(self_obj, 2, 3);
   call Node999;
end
