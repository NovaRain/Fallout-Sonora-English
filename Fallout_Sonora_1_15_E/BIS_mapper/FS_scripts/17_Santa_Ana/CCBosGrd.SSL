#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_CCBosGrd
#define TOWN_REP_VAR                    GVAR_TOWN_REP_SANTA_ANA // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_SANTA_ANA_CRCH   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_MEXIC_GEN      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      35 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

#define TILE_CHRCH                      19506  // точка размещения в церкви

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure mapLvlPosition;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Alcoghol                   (6) // алкоголь: 0 - не использован, 1 - использован

import variable box_stop_use;

variable flagAttak;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_local_var(LVAR_Alcoghol,0);

       if (self_elevation > 0) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SANTA_ANA_CRCH);
       end
       set_obj_visibility(self_obj, 0);
       if (global_var(GVAR_SANTAANA_STATUS_BOSS) != 0) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
          destroy_object(self_obj);
       end
       // added to allow for combat taunts to be either pure spanish, or 'translated' via the Espanol Trait - cambragol
       if DudeTraitHas(TRAIT_espanol) then begin
          critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_MEXIC_GEN_ENG);
       end
   end
end

procedure mapLvlPosition begin
   if (self_elevation == 0) then begin
      if (global_var(GVAR_SANTAANA_STATUS_BOSS) == 0) then begin
          if ((global_var(GVAR_SANTAANA_STATUS_FIESTA) >= 2) or (map_var(MVAR_SANTA_BOSS_ATTAK) == 1) or (global_var(GVAR_SANTAANA_STATUS_EL_DIABLO) != 0)) then begin
          critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_SANTA_ANA_CRCH);
          set_local_var(LVAR_Home_Tile, TILE_CHRCH);
          set_local_var(LVAR_Home_Rotat,5);
          move_to(self_obj, HomeTile, 2);
          anim(self_obj, ANIMATE_ROTATION, HomeRotat);
          end
      end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (self_elevation == 2) then begin
       timer_move_random_home(6)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   flagAttak := 1;
   if ((self_elevation > 0) and (self_elevation == dude_elevation)) then begin
      flagAttak := 2;
      if (tile_distance(dude_tile,self_tile)<=3) then begin
         if (tile_distance(dude_tile,16703)<=2) then begin
         flagAttak := 1;
         end
         else if (tile_distance(dude_tile,22706)<=2) then begin
         flagAttak := 1;
         end
      end
   end
   if (global_var(GVAR_SANTAANA_STATUS_FIESTA) == 10) then begin
   flagAttak := 1;
   end
   if (map_var(MVAR_SANTA_BOSS_ATTAK)>0) then begin
   flagAttak := 2;
   end
   if (getQuest(GVAR_SANTAANA_QST_BOSS) >= qEnd) then begin
   flagAttak := 1;
   end

   if (local_var(LVAR_Alcoghol) == 0) then begin
   set_using_stop
   end
   set_critter_options(flagAttak,0)
   call mapLvlPosition;
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

//This script was tweaked to standardize handling of the espanol perk - cambragol

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 2) then begin
       display_mstr(103);
         if DudeTraitHas(TRAIT_espanol) then begin
            z_floater(self_obj,103,COLOR_MSG_GREEN);
         end
         else begin
            z_floater(self_obj,102,COLOR_MSG_GREEN);
         end
       box_stop_use := 0;
       inc_local_var(LVAR_Alcoghol);
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
      if (self_elevation == 0) then begin
      set_REP(REP_KILL_CRITICAL)
      end
   end
   stop_santa_fiesta
   kill_critter_reput
end

procedure talk_p_proc begin
   if (self_elevation == 0) then begin
      if DudeTraitHas(TRAIT_espanol) then begin
      floater(self_obj, random(121,123), COLOR_MSG_RED);
      end
      else begin
      floater(self_obj, random(111,112), COLOR_MSG_RED);
      end
   end
   else begin
      if DudeTraitHas(TRAIT_espanol) then begin
      floater(self_obj, 120, COLOR_MSG_RED);
      end
      else begin
      floater(self_obj, 110, COLOR_MSG_RED);
      end
   end
end
