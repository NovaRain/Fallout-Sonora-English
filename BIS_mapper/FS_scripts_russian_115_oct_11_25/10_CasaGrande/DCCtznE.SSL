#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_DCCtznE
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_GRANDE // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASAGRANDE_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      30 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeStopDialog;
procedure NodeFstDialog;
procedure NodeFstDialog102;
procedure NodeFstDialog103;
procedure NodeFstDialog104;
procedure NodeFstDialog105;
procedure NodeFstDialog106;
procedure NodeFstDialog107;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;
procedure NodeAnswer106;
procedure NodeAnswer107;
procedure NodeAnswer108;
procedure NodeAnswer109;
procedure NodeAnswer110;
procedure NodeAnswer111;
procedure NodeAnswer112;
procedure NodeAnswer113;
procedure NodeAnswer114;
procedure NodeAnswer115;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer202;
procedure NodeAnswer203;
procedure NodeAnswer204;
procedure NodeAnswer300;
procedure NodeAnswer301;
procedure NodeAnswer302;
procedure NodeAnswer303;
procedure NodeAnswer304;
procedure NodeAnswer305;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(4)
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 3) then begin
      setBonusReaction(5)
      if DudeTraitHas(TRAIT_espanol) then begin
      z_floater(self_obj, 252, COLOR_MSG_GREEN);
      end
      else begin
      z_floater(self_obj, 253, COLOR_MSG_GREEN);
      end
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_CGRANDE_STATUS_CTZN);
   setQuest(GVAR_CGRANDE_QST_SALUT,qZero);
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-55) // Поправка к отношению за принадлежность к фракциям
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (town_rep_is_hated or (get_SLAVA <= -50) or (getReaction <= -50) or (armor_pid_dude_var == PID_ARMOR_ROAD) or (global_var(GVAR_TITUL_WARRIOR_ROAD) > 0)) then begin
      call NodeStopDialog;
   end
   else if (bitDialog(bit_1) == 0) then begin
      call NodeFstDialog;
   end
   else begin
      if (bitDialog(bit_7) == 0) then begin
      call NodeSndDialog2;
      end
      else begin
      call NodeSndDialog;
      end
   end
end

procedure NodeStopDialog begin
   if DudeTraitHas(TRAIT_espanol) then begin
   zReply(240)
   end
   else begin
   Reply(243)
   end
   zNAnswer(241,0,Node999)
   NAnswer(242,0,Node999)
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(201,0,NodeFstDialog102)
end

procedure NodeFstDialog102 begin
   Reply(202)

   BAnswer(204,3,NodeFstDialog103)
   if DudeTraitHas(TRAIT_espanol) then begin
   GAnswer(208,3,NodeFstDialog105)
   end
   else begin
   NAnswer(210,3,NodeFstDialog104)
   end

   NAnswer(203,0,Node999)
end

procedure NodeFstDialog103 begin
   Reply(205)

   if DudeTraitHas(TRAIT_espanol) then begin
   GAnswer(208,3,NodeFstDialog105)
   BAnswer(219,7,Node999)
   end
   else begin
   NAnswer(210,3,NodeFstDialog104)
   BAnswer(218,0,Node999)
   end

   NAnswer(203,0,Node999)
end

procedure NodeFstDialog104 begin
   Reply(211)
   NAnswer(212,0,Node999)
end

procedure NodeFstDialog105 begin
   bitDialogExp(bit_1,50)
   setBonusReaction(70)
   set_REP(1)
   Reply(209)
   NAnswer(213,0,NodeFstDialog107)
   BAnswer(214,0,NodeFstDialog106)
end

procedure NodeFstDialog106 begin
   setBonusReaction(-10)
   set_REP(-1)
   call NodeFstDialog107;
end

procedure NodeFstDialog107 begin
   call NodeSndDialog;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   //(титул на арене, высокая сила, навык рукопашки, силовая броня) Вы, мордовороты, совсем не цените мир и покой. Чего тебе, hombre-martillo?
   Reply(250)

   if (bitDialog(bit_3) == 0) then begin
   NAnswer(260,3,NodeAnswer100)
   end
   if (bitDialog(bit_4) == 0) then begin
   NAnswer(290,3,NodeAnswer200)
   end
   if (bitDialog(bit_5) == 0) then begin
   NAnswer(320,3,NodeAnswer300)
   end
   if (bitDialog(bit_6) == 0) then begin
   if (bitDialog(bit_5) != 0) then begin
      if (global_var(GVAR_CGRANDE_STATUS_MER) >= 0) then begin
      NAnswer(327,4,NodeAnswer302)
      end
      else begin
      NAnswer(341,4,NodeAnswer305)
      end
   end
   end

   if (bitDialog(bit_3) != 0) then begin
   if (getQuest(GVAR_CGRANDE_QST_SALUT) == qZero) then begin
   NAnswerReaction(350,5,NodeAnswer113,10,350,NodeAnswer115)
   end
   end

   NAnswer(251,0,Node999)
end

procedure NodeSndDialog2 begin
   bitDialogExp(bit_7,0)
   if DudeTraitHas(TRAIT_espanol) then begin
   zReply(252)
   end
   else begin
   zReply(253)
   end
   NAnswer(254,0,NodeSndDialog)
   BAnswer(255,0,NodeSndDialog)
end

//--- КТО ТЫ?

procedure NodeAnswer100 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(261)
   NAnswer(265,4,NodeAnswer101)
   BAnswer(266,4,NodeAnswer102)
   NAnswer(263,0,NodeSndDialog)
   NAnswer(264,0,Node999)
   end
   else begin
   Reply(262)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer101 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(267)
   GAnswer(269,4,NodeAnswer105)
   NAnswer(270,4,NodeAnswer103)
   BAnswer(271,4,NodeAnswer104)
   //NAnswer(272,0,NodeSndDialog)
   //NAnswer(273,0,Node999)
   end
   else begin
   Reply(268)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer102 begin
   setBonusReaction(-10)
   set_REP(-1)
   call NodeAnswer101;
end

procedure NodeAnswer103 begin
   call NodeAnswer106;
end

procedure NodeAnswer104 begin
   setBonusReaction(-10)
   set_REP(-1)
   call NodeAnswer103;
end

procedure NodeAnswer105 begin
   setBonusReaction(10)
   set_REP(1)
   call NodeAnswer103;
end

procedure NodeAnswer106 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(274)
   GAnswer(278,0,NodeAnswer108)
   NAnswer(276,0,NodeAnswer109)
   BAnswer(277,0,NodeAnswer107)
   end
   else begin
   Reply(275)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer107 begin
   setBonusReaction(-10)
   set_REP(-1)
   call NodeAnswer109;
end

procedure NodeAnswer108 begin
   setBonusReaction(10)
   set_REP(1)
   call NodeAnswer109;
end

procedure NodeAnswer109 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(279)
   NAnswer(281,0,NodeAnswer110)
   end
   else begin
   Reply(280)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer110 begin
   bitDialogExp(bit_3,50)
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(282)
   GAnswer(284,0,NodeAnswer112)
   NAnswer(285,0,NodeSndDialog)
   BAnswer(286,0,NodeAnswer111)
   //NAnswer(287,0,Node999)
   end
   else begin
   Reply(283)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer111 begin
   setBonusReaction(-10)
   set_REP(-1)
   call NodeSndDialog;
end

procedure NodeAnswer112 begin
   setBonusReaction(10)
   set_REP(1)
   call NodeSndDialog;
end

procedure NodeAnswer113 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(351)
   NAnswer(353,0,NodeAnswer114)
   end
   else begin
   Reply(352)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer114 begin
   setQuest(GVAR_CGRANDE_QST_SALUT,qStart);
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(354)
   NAnswer(356,0,NodeSndDialog)
   NAnswer(357,0,Node999)
   end
   else begin
   Reply(355)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer115 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(358)
   NAnswer(360,0,NodeSndDialog)
   NAnswer(361,0,Node999)
   end
   else begin
   Reply(359)
   NAnswer(212,0,Node999)
   end
end

//--- ЧЕМ ЗАНИМАЕШЬСЯ?

procedure NodeAnswer200 begin
   if DudeTraitHas(TRAIT_espanol) then begin
      if (global_var(GVAR_CGRANDE_STATUS_BATHHOUSE) >= 2) then begin
      Reply(291)
      end
      else if (global_var(GVAR_CGRANDE_STATUS_MER) >= 0) then begin
      Reply(292)
      end
      else begin
      Reply(293)
      end
      NAnswer(297,5,NodeAnswer201)
      NAnswer(295,0,NodeSndDialog)
      NAnswer(296,0,Node999)
   end
   else begin
      Reply(294)
      NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer201 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(298)
   end
   else begin
   Reply(299)
   end

   if (map_is_known(MAP_SAN_BRAHMIN_GARD) == true) then begin
   BAnswer(302,3,NodeAnswer202)
   end
   else if (map_is_known(MAP_FLAGSTAFF_CAVE) == true) then begin
   BAnswer(303,3,NodeAnswer202)
   end
   else begin
   BAnswer(304,3,NodeAnswer202)
   end
   GAnswerSkill(314,5,NodeAnswer204,SKILL_CONVERSANT,50,309,NodeAnswer203)
   NAnswer(300,0,NodeSndDialog)
   NAnswer(301,0,Node999)
end

procedure NodeAnswer202 begin
   bitDialogExp(bit_4,25)
   setBonusReaction(-10)
   set_REP(-1)
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(305)
   NAnswer(307,0,NodeSndDialog)
   NAnswer(308,0,Node999)
   end
   else begin
   Reply(306)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer203 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(310)
   NAnswer(312,0,NodeSndDialog)
   NAnswer(313,0,Node999)
   end
   else begin
   Reply(311)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer204 begin
   bitDialogExp(bit_4,35)
   setBonusReaction(10)
   set_REP(1)
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(315)
   end
   else begin
   Reply(316)
   end
   NAnswer(317,0,NodeSndDialog)
   NAnswer(318,0,Node999)
end

//--- ПРО ГОРОД

procedure NodeAnswer300 begin
   bitDialogExp(bit_5,0)
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(321)
   NAnswer(323,0,NodeAnswer301)
   end
   else begin
   Reply(322)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer301 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(324)
   NAnswer(326,0,NodeSndDialog)
   end
   else begin
   Reply(325)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer302 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(328)
   NAnswerSkill(337,5,NodeAnswer304,SKILL_CONVERSANT,70,332,NodeAnswer303)
   NAnswer(330,0,NodeSndDialog)
   NAnswer(331,0,Node999)
   end
   else begin
   Reply(329)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer303 begin
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(333)
   NAnswer(335,0,NodeSndDialog)
   NAnswer(336,0,Node999)
   end
   else begin
   Reply(334)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer304 begin
   bitDialogExp(bit_6,50)
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(338)
   NAnswer(340,0,NodeSndDialog)
   end
   else begin
   Reply(339)
   NAnswer(212,0,Node999)
   end
end

procedure NodeAnswer305 begin
   bitDialogExp(bit_6,0)
   if DudeTraitHas(TRAIT_espanol) then begin
   Reply(342)
   NAnswer(344,0,NodeSndDialog)
   end
   else begin
   Reply(343)
   NAnswer(212,0,Node999)
   end
end
