#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_AFGhoul
#define TOWN_REP_VAR                    GVAR_TOWN_REP_OIL_PLATFORM // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_GHOUL_WILD       // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GHOUL_ZOMBIE       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GHOUL      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_BAD          // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog;
procedure NodeDialog2;
procedure NodeDialog3;
procedure NodeDialog4;
procedure NodeDialog5;
procedure NodeDialog6;

variable attakToDude;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_local_var(LVAR_Home_Rotat,random(0,5));
       set_obj_visibility(self_obj, 1);
       if (map_var(MVAR_OIL_PLATFORM_GHOUL_INVIS) != 0) then begin
       set_map_var(MVAR_OIL_PLATFORM_GHOUL_INVIS,0);
       end
       if (global_var(GVAR_OIL_STATUS_GOLD) == 1) then begin
       set_map_var(MVAR_OIL_PLATFORM_GHOUL_INVIS,1);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(6)
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   if ((game_time_hour < 0600) and (map_var(MVAR_OIL_PLATFORM_GHOUL_INVIS) == 0)) then begin
   invisToDude(7,101) // Скрывание нпс от ГГ до некоторой дистанции, в скобках реплика для лога (устанавливать set_obj_visibility(self_obj, 1) в map_enter_p_proc)
   end
   attakToDude := 2;
   if (map_var(MVAR_OIL_PLATFORM_GHOUL_INVIS) > 0) then begin
   attakToDude := 0;
   end
   else if ((attakToDude == 99) or (dude_charisma <= 2) or DudePerkHas(PERK_ghouling_charisma)) then begin
   attakToDude := 0;
   end
   else if (global_var(GVAR_OIL_STATUS_GHOUL) > 0) then begin
   attakToDude := 0;
   end
   set_critter_options(attakToDude,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if ((objUsedPid == PID_M_ASH) or (objUsedPid == PID_M_MENTATS)) then begin
       if (global_var(GVAR_OIL_STATUS_GHOUL) == 0) then begin
       script_overrides;
       item_remove(dude_obj, objUsedPid, 1)
       if (combat_is_initialized == true) then begin
       terminate_combat;
       end
       off_enemy_dude;
       set_local_var(LVAR_Flags,0);
       attakToDude := 99;
       dialogue_system_enter;
       end
   end
end

procedure combat_p_proc begin
   effectAttak(0,4,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_OIL_STATUS_GHOUL, -1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_OIL_STATUS_GHOUL) == 0) then begin
      if ((attakToDude == 99) or (dude_charisma <= 2) or DudePerkHas(PERK_ghouling_charisma)) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
      end
      else begin
      floater(self_obj,102,COLOR_MSG_RED);
      end
   end
   else begin
      floater(self_obj,102,COLOR_MSG_RED);
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   attakToDude := 0;
   call NodeDialog;
end

procedure NodeDialog begin
   Reply(200)
   NAnswer(202,0,NodeDialog2)
   BAnswer(201,0,Node998)
end

procedure NodeDialog2 begin
   Reply(203)
   NAnswer(205,0,NodeDialog3)
   BAnswer(204,0,Node998)
end

procedure NodeDialog3 begin
   Reply(206)
   NAnswer(207,0,NodeDialog4)
   BAnswer(210,0,Node998)
end

procedure NodeDialog4 begin
   Reply(208)
   NAnswer(209,0,NodeDialog5)
   BAnswer(211,0,Node998)
end

procedure NodeDialog5 begin
   Reply(212)
   NAnswer(213,0,NodeDialog6)
end

procedure NodeDialog6 begin
   bitDialogExp(bit_1,100)
   set_global_var(GVAR_OIL_STATUS_GHOUL, 1);
   set_map_var(MVAR_OIL_PLATFORM_GHOUL_INVIS,1);
   set_obj_visibility(self_obj, 1);
   call Node999;
end
