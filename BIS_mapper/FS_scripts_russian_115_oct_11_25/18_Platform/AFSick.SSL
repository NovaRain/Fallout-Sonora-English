#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_AFSick
#define TOWN_REP_VAR                    GVAR_TOWN_REP_OIL_PLATFORM // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_OIL_PLATFORM_CTZN  // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure uninjure_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Medic_Lvl                     (5) // уровень, необходимый для лечения через навык

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_first_run == true) then begin
       set_local_var(LVAR_Medic_Lvl, random(35,75));
       end
   end
end

procedure look_at_p_proc begin
   if (get_Medic_Quest == 0) then begin
   script_overrides;
   display_mstr(102);
   end
   else begin
   lookProcMsg
   end
end

procedure description_p_proc begin
   if (get_Medic_Quest == 0) then begin
   script_overrides;
   display_mstr(103);
   end
   else begin
   dsrptProcMsg
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
      if (get_Medic_Quest == 0) then begin
         timer_float_reply(110,111)
      end
      else begin
         //timer_float_reply(120,121)
      end
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
   if (action_being_used == SKILL_DOCTOR) then begin
      if (get_Medic_Quest == 0) then begin
         script_overrides;
         if (has_skill(dude_obj, SKILL_DOCTOR) < local_var(LVAR_Medic_Lvl)) then begin
         skill_display_mstr(104,SKILL_DOCTOR,local_var(LVAR_Medic_Lvl))
         end
         else begin
         call uninjure_proc;
         end
      end
   end
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (objUsedPid == PID_M_ANTIDOTE) then begin
      if (get_Medic_Quest == 0) then begin
      //script_overrides;
      call uninjure_proc;
      end
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure uninjure_proc begin
   if (get_Medic_Quest == 0) then begin
      PLAY_FADE
      inc_map_var(MVAR_OIL_PLATFORM_POISON);
      /*
      if (local_var(LVAR_Medic_Lvl)>0) then begin
      exp_add(local_var(LVAR_Medic_Lvl)*3);
      end
      */
      display_msg(mstr(105)+map_var(MVAR_OIL_PLATFORM_POISON)+mstr(106));
      floater(self_obj, 107, COLOR_MSG_GREEN);
      set_Medic_Quest;
   end
end

procedure talk_p_proc begin
   if (get_Medic_Quest == 0) then begin
      if town_rep_is_hated then begin
      floater(self_obj, 121, COLOR_MSG_RED);
      end
      else begin
      floater(self_obj, 120, COLOR_MSG_RED);
      end
   end
   else begin
      floatOilGhoulsAttak
      else if town_rep_is_hated then begin
      floater(self_obj, 123, COLOR_MSG_NORMAL);
      end
      else begin
      floater(self_obj, 122, COLOR_MSG_NORMAL);
      end
   end
end
