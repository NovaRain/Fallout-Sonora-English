#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Отель: сутенер-таксист-пушер Деррик

#define NAME                            SCRIPT_YCSantPs
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW
#define BOX_TILE                        11  // номер тайла с личным товарным ящиком (-1, если не надо; не использовать значения <10; не может совпадать с чужими ящиками на одной локации)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GHOUL_GENERIC      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure setBarterA;
procedure setBarterB;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeSndDialog;
procedure NodeGo;
procedure NodeGo2;
procedure NodeGo3;
procedure NodeGo3_2;
procedure NodeGo4;
procedure NodeGo5;
procedure NodeBarter;
procedure NodeQstDialog100;
procedure NodeQstDialog101;
procedure NodeQstDialog102;
procedure NodeQstDialog103;
procedure NodeQstDialog104;

//---------------------

//#define LVAR_Flag_A                     (8) // отвечает за ...

variable mapIndx;
variable mapCoast;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_var(MVAR_DIEGOPIRS_DESANTI_PUSHER) >= 2) then begin
       if (map_var(MVAR_DIEGOPIRS_DESANTI_SID)>0) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_DAYGLOW_GHOUL);
       end
       end
       set_BarterBoxProc(ONE_GAME_DAY*3)
       if (map_var(MVAR_DIEGOPIRS_DESANTI) < 0) then begin
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure setBarterA begin
        check_restock_item(PID_M_PSYCHO, 1, 1, 100)
        check_restock_item(PID_M_MENTATS, 1, 1, 100)
        check_restock_item(PID_M_BUFFOUT, 1, 1, 100)
        check_restock_item(PID_M_ASH, 2, 2, 100)
        check_restock_item(PID_M_MEX_BEER, 3, 3, 100)

        //- крышки
        check_restock_many(400,500)
end

procedure setBarterB begin
   //! пока пусто
   set_BarterDopusk;
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(5)
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       if (mapIndx>0) then begin
          if (mapIndx == MAP_DAYGLOW_CAMP_SAN_ISIDRO) then begin
          set_global_var(GVAR_DAYGLOW_MONORAIL_MOVETO, MAP_DAYGLOW_CAMP_SAN_ISIDRO+1000);
          end
          dude_teleport_map(0,0,mapIndx,2);
       end
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      if (self_cur_hits <= (self_max_hits/2)) then begin
      if (map_var(MVAR_DIEGOPIRS_DESANTI_PUSHER) == 1) then begin
      if (map_var(MVAR_DIEGOPIRS_DESANTI_SID) == 0) then begin
         critter_set_flee_state(self_obj, 1);
         set_local_var(LVAR_Flags,0);
         set_local_var(LVAR_Self_Flags,0);
         off_enemy_dude;
         display_mstr(104);
         floater(self_obj,103,COLOR_MSG_RED);
         set_map_var(MVAR_DIEGOPIRS_DESANTI_SID, 1);
         terminate_combat;
      end
      end
      end
   end
   effectAttak(0,5,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   //effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_map_var(MVAR_DIEGOPIRS_DESANTI_SID, -1);
   kill_critter_opt
   kill_critter_box
end

procedure talk_p_proc begin
   if (map_var(MVAR_DIEGOPIRS_DESANTI) < 0) then begin
   floater(self_obj, 102, COLOR_MSG_RED);
   end
   else if ((map_var(MVAR_DIEGOPIRS_DESANTI) >= 0) and (map_var(MVAR_DIEGOPIRS_DESANTI_PUSHER) == 1) and (map_var(MVAR_DIEGOPIRS_DESANTI_SID) == 1)) then begin
   floater(self_obj,103,COLOR_MSG_RED);
   end
   else begin
   setReaction(-1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,20) // поправка к отношению за принадлежность к фракции
   dialog_proc_barter(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   Reply(210)
   end
   else begin
   Reply(200)
   end
   NAnswer(205,0,NodeFstDialog3)
   BAnswer(201,0,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(204,0,NodeFstDialog3)
   NAnswer(203,0,Node999)
end

procedure NodeFstDialog3 begin
   bitDialogExp(bit_1,0)
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   Reply(208)
   end
   else begin
   Reply(206)
   end
   NAnswer(207,0,NodeBarter)
   NAnswer(214,0,NodeGo)
   NAnswer(209,0,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(220)
   if (map_var(MVAR_DIEGOPIRS_DESANTI_PUSHER) == 1) then begin
   if (map_var(MVAR_DIEGOPIRS_DESANTI_SID) == 0) then begin
   NAnswer(230,0,NodeQstDialog100)
   end
   end
   NAnswer(250,0,NodeGo)
   NAnswer(222,0,NodeBarter)
   NAnswer(221,0,Node999)
end

procedure NodeGo begin
   Reply(251)
   if DudePerkHas(PERK_ghouling_fiziology) then begin
      NAnswer(mstr(252)+1000+mstr(254),0,NodeGo2)
      NAnswer(mstr(259)+500+mstr(254),0,NodeGo3_2)
      if ((getQuest(GVAR_DAYGLOW_MAIN_A) > qZero) or (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_28) != 0)) then begin
      NAnswer(mstr(253)+500+mstr(254),0,NodeGo3)
      end
   end
   else begin
      NAnswer(mstr(252)+1500+mstr(254),0,NodeGo2)
      NAnswer(mstr(259)+750+mstr(254),0,NodeGo3_2)
      if ((getQuest(GVAR_DAYGLOW_MAIN_A) > qZero) or (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_28) != 0)) then begin
      NAnswer(mstr(253)+750+mstr(254),0,NodeGo3)
      end
   end
   NAnswer(223,0,NodeBarter)
   NAnswer(255,0,NodeSndDialog)
end

procedure NodeGo2 begin
   mapIndx := MAP_DAYGLOW_CAMP_SAN_ISIDRO;
   mapCoast := 1500;
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   mapCoast := 1000;
   end
   call NodeGo4;
end

procedure NodeGo3 begin
   mapIndx := MAP_DAYGLOW_BONITA_DUM;
   mapCoast := 750;
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   mapCoast := 500;
   end
   call NodeGo4;
end

procedure NodeGo3_2 begin
   mapIndx := MAP_DAYGLOW_CHULAVISTA;
   mapCoast := 750;
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   mapCoast := 500;
   end
   call NodeGo4;
end

procedure NodeGo4 begin
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) < mapCoast) then begin
   Reply(256)
   NAnswer(258,0,NodeSndDialog)
   NAnswer(257,0,Node999)
   end
   else begin
   call NodeGo5;
   end
end

procedure NodeGo5 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,mapCoast)
   add_timer_event(self_obj, 2, 3);
   call Node999;
end

procedure NodeBarter begin
   goBarterMod;
   Reply(211)
   if (map_var(MVAR_DIEGOPIRS_DESANTI_PUSHER) == 1) then begin
   if (map_var(MVAR_DIEGOPIRS_DESANTI_SID) == 0) then begin
   NAnswer(230,0,NodeQstDialog100)
   end
   end
   NAnswer(250,0,NodeGo)
   NAnswer(212,0,Node999)
   NAnswer(213,0,Node999)
end

procedure NodeQstDialog100 begin
   Reply(231)
   NAnswer(232,0,NodeQstDialog101)
   NAnswer(234,0,Node999)
end

procedure NodeQstDialog101 begin
   Reply(233)
   NAnswerSkill(237,3,NodeQstDialog102,SKILL_CONVERSANT,50,236,Node999)
   BAnswer(240,0,Node998)
   NAnswer(234,0,Node999)
end

procedure NodeQstDialog102 begin
   Reply(238)
   NAnswer(241,0,NodeQstDialog103)
   BAnswer(240,0,Node998)
   NAnswer(239,0,Node999)
end

procedure NodeQstDialog103 begin
   Reply(242)
   NAnswer(243,0,NodeQstDialog104)
   BAnswer(240,0,Node998)
   NAnswer(239,0,Node999)
end

procedure NodeQstDialog104 begin
   set_map_var(MVAR_DIEGOPIRS_DESANTI_SID, 1);
   Reply(244)
   NAnswer(246,0,Node999)
   BAnswer(245,0,Node998)
end
