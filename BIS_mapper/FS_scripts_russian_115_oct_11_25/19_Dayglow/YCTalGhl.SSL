#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCTalGhl
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GHOUL_ZOMBIE       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure moveGhoulMap;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

//#define LVAR_Flag_A                     (5) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);
       if (global_var(GVAR_DAYGLOW_STATUS_GONZO) > 0) then begin
          call moveGhoulMap;
          set_obj_visibility(self_obj, 0);
       end
   end
end

procedure moveGhoulMap begin
   variable doorObj;
   if (map_var(MVAR_DIEGO_A_GHOST) == 0) then begin
       if (tile_contains_obj_pid(25891,0,33555331) == true) then begin
           destroy_object(tile_contains_pid_obj(25891, 0, 33555331));
           doorObj := create_object_sid(33555331, 25891, 0, SCRIPT_ZSDoor);
           obj_lock(doorObj);
           add_timer_event(doorObj, 2, 1);
       end
       move_to(dude_obj, 24695, 0);
       if (partyPidMtn != 0) then begin
       move_to(partyPidMtn, tile_num_in_direction(dude_tile,3,1), 0);
       end
       set_map_var(MVAR_DIEGO_A_GHOST, 1);
   end
end


procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if ((map_var(MVAR_DIEGO_A_GHOST) == -100) and (global_var(GVAR_DAYGLOW_STATUS_GONZO) > 0)) then begin
       timer_move_random
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //
   end
end

procedure critter_p_proc begin
   if (map_var(MVAR_DIEGO_A_GHOST) == -100) then begin
   set_critter_options(0,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
   set_critter_options(2,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,2,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_map_var(MVAR_DIEGO_A_GHOST, -1);
   set_global_var(GVAR_DAYGLOW_STATUS_GONZO, -1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (map_var(MVAR_DIEGO_A_GHOST) < 0) then begin
   floater(self_obj, 103, COLOR_MSG_RED);
   end
   else begin
   script_overrides;
   display_mstr(102);
   end
end
