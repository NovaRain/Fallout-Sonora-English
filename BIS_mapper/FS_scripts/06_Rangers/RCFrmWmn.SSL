#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_RCFrmWmn
#define TOWN_REP_VAR                    GVAR_TOWN_REP_RANGERS // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_RANGERS          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog1;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog6;
procedure NodeSndDialog;
procedure NodeSndDialog2;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (game_time_hour < 0600) then begin
       timer_move_to_tile(0,15873,5)
       end
       else begin
       timer_move_random_home(4)
       end
   end
   if (fixed_param == 2) then begin
       if (game_time_hour < 0600) then begin
          timer_float_reply(130,135)
       end
       else if (random(0,2) == 1) then begin
          if (getQuest(GVAR_RANGERS_DUMB_DESTROY) >= qEnd) then begin
          timer_float_reply(125,129)
          end
          else begin
          timer_float_reply(120,124)
          end
       end
       else begin
          timer_float_reply(110,119)
       end
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)

   fracReaction(GVAR_TITUL_ARIZONA_RANGER,50) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_PHENIX_CULT,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS_TRAITOR,20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-20) // Поправка к отношению за принадлежность к фракциям

   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else if ((getQuest(GVAR_RANGERS_DUMB_DESTROY) >= qEnd) or town_rep_is_hated or (getReaction <= -15)) then begin
   call NodeSndDialog2;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(dude_name+mstr(201),1,NodeFstDialog1)
   NAnswer(202,1,NodeFstDialog2)
end

procedure NodeFstDialog1 begin
   setBonusReaction(5)
   Reply(mstr(203)+dude_name+mstr(204))
   NAnswer(206,1,NodeFstDialog5)
   BAnswer(207,1,NodeFstDialog3)
end

procedure NodeFstDialog2 begin
   setBonusReaction(-5)
   Reply(mstr(205)+mstr(204))
   NAnswer(206,1,NodeFstDialog5)
   BAnswer(207,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   setBonusReaction(-5)
   Reply(208)
   NAnswer(209,1,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   Reply(210)
   NAnswer(211,1,NodeFstDialog5)
end

procedure NodeFstDialog5 begin
   Reply(212)
   NAnswer(213,1,NodeFstDialog6)
   NAnswer(214,1,Node999)
end

procedure NodeFstDialog6 begin
   bitDialogExp(bit_2,0)
   Reply(215)
   NAnswer(214,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(220)
   if (bitDialog(bit_2) == 0) then begin
   NAnswer(213,1,NodeFstDialog6)
   end
   NAnswer(221,1,Node999)
end

procedure NodeSndDialog2 begin
   zReply(222)
   BAnswer(223,1,Node999)
   NAnswer(224,1,Node999)
end
