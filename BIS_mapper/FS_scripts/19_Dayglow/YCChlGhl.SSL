#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCChlGhl
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GHOUL_ZOMBIE       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      70 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog100;
procedure NodeDialog101;
procedure NodeDialog102;
procedure NodeDialog103;

//---------------------

#define LVAR_Talk                     (6) // готовность гуля к диалогу

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home

       if (random(1,3) == 1) then begin
       set_local_var(LVAR_Home_Tile,22495);
       end
       if (random(1,3) == 1) then begin
       set_local_var(LVAR_Home_Tile,21715);
       end
       if (random(1,3) == 1) then begin
       set_local_var(LVAR_Home_Tile,15512);
       end
       if (map_var(MVAR_DIEGOCHULA_HOLO) == 2) then begin
       set_local_var(LVAR_Home_Tile,19698);
       set_local_var(LVAR_Home_Rotat,5);
       end

       move_to(self_obj, HomeTile, self_elevation);

       set_obj_visibility(self_obj, 0);
       if ((global_var(GVAR_DAYGLOW_STATUS_GONZO) != 0) or (getQuest(GVAR_DAYGLOW_A_GHOST) >= qEnd) or (getQuest(GVAR_DAYGLOW_A_GHOST) <= qStop)) then begin
       set_obj_visibility(self_obj, 1);
       destroy_object(self_obj);
       end

   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (map_var(MVAR_DIEGOCHULA_HOLO) > 0) then begin
       timer_move_to_home
       end
       else begin
       timer_move_random
       end
   end
   if (fixed_param == 2) then begin
       //!!!
   end
   if (fixed_param == 3) then begin
       set_global_var(GVAR_DAYGLOW_STATUS_GONZO, 1);
       dude_teleport_map(0,0,MAP_DAYGLOW_CAMP_SAN_ISIDRO,1);
   end
end

procedure critter_p_proc begin
   if (map_var(MVAR_DIEGOCHULA_HOLO) == 1) then begin
       if (combat_is_initialized == true) then begin
       terminate_combat;
       end
       off_enemy_dude;
       set_local_var(LVAR_Flags,0);
       set_local_var(LVAR_Home_Tile,19698);
       set_local_var(LVAR_Home_Rotat,5);
       set_map_var(MVAR_DIEGOCHULA_HOLO, 2);
   end

   if ((map_var(MVAR_DIEGOCHULA_HOLO) >= 2) or (local_var(LVAR_Talk) > 0)) then begin
   set_critter_options(0,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
   set_critter_options(2,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if ((objUsedPid == PID_M_ASH) or (objUsedPid == PID_M_MENTATS)) then begin
       script_overrides;
       item_remove(dude_obj, objUsedPid, 1)
       if (combat_is_initialized == true) then begin
       terminate_combat;
       end
       off_enemy_dude;
       set_local_var(LVAR_Flags,0);
       set_local_var(LVAR_Talk,1);
   end
end

procedure combat_p_proc begin
   effectAttak(0,2,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_DAYGLOW_STATUS_GONZO, -1);
   if (partyPidMtn != 0) then begin
   floater(partyPidMtn, 104, COLOR_MSG_NORMAL);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_DAYGLOW_STATUS_GONZO) > 0) then begin
   script_overrides;
   display_mstr(102);
   end
   else if ((map_var(MVAR_DIEGOCHULA_HOLO) >= 2) or (local_var(LVAR_Talk) > 0)) then begin
   setReaction(0) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,90) // поправка к отношению за принадлежность к фракции
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else begin
   floater(self_obj, 103, COLOR_MSG_RED);
   end
end

procedure Node998 begin
   setNodeCombat;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   call NodeDialog100;
end

procedure NodeDialog100 begin
   Reply(200)
   NAnswer(201,1,NodeDialog101)
   BAnswer(207,1,Node998)
end

procedure NodeDialog101 begin
   Reply(202)
   NAnswer(203,1,NodeDialog102)
   BAnswer(208,1,Node998)
end

procedure NodeDialog102 begin
   Reply(204)
   GAnswer(205,1,NodeDialog103)
   NAnswer(206,1,Node999)
end

procedure NodeDialog103 begin
   add_timer_event(self_obj, 3, 3);
   call Node999;
end
