#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCCtzn
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GHOUL_GENERIC      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      10 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Flag_Dinner                  (5) // получение обеда от бармена

import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 0);
       if (map_first_run) then begin
          if (cur_map_index == MAP_DAYGLOW_CAMP_SAN_ISIDRO) then begin
          item_add(self_obj,PID_BOOTLE_CAPS,random(1,15))
          end
          if (cur_map_index == MAP_DAYGLOW_CAMP_OLD_TOWN) then begin
          item_add(self_obj,PID_BOOTLE_CAPS,random(1,20))
          end
       end
       if (cur_map_index == MAP_DAYGLOW_BONITA_DUM) then begin
          if ((map_var(MVAR_DIEGODUM_ROBOTS) == 0) and (getQuest(GVAR_DAYGLOW_MAIN_A) >= qEnd)) then begin
          set_map_var(MVAR_DIEGODUM_ROBOTS,1);
          end
          if ((getQuest(GVAR_DAYGLOW_MAIN_A) < qEnd) or (map_var(MVAR_DIEGODUM_ROBOTS) <= 0)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
          end
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (cur_map_index == MAP_DAYGLOW_BONITA_DUM) then begin
          timer_move_random
       end
       else begin
          if (random(0,2) == 0) then begin
          timer_move_random_home(4)
          end
          else begin
          add_timer_event(self_obj, 100, 1);
          end
       end
   end
   if (fixed_param == 2) then begin
       /*if DudePerkHas(PERK_ghouling_fiziology) then begin
       timer_float_reply(120,129)
       end
       else if (random(0,100) >= 50) then begin
       timer_float_reply(120,129)
       end
       else begin
       timer_float_reply(110,119)
       end*/
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if ((objUsedPid == PID_Q_DAYGLOW_BORDERBADGE_A) or (objUsedPid == PID_Q_DAYGLOW_BORDERBADGE_B)) then begin
      script_overrides;
      floater(self_obj, random(150,156), COLOR_MSG_NORMAL);
   end
   if (objUsedPid == PID_M_TV_DINNER) then begin
      if (cur_map_index == MAP_DAYGLOW_BONITA_DUM) then begin
      script_overrides;
      if ((getQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER) > qZero) and (getQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER) < 10)) then begin
         if (lvar_bit(LVAR_Self_Flags, bit_20) == false) then begin
         display_msg(mstr(107)+global_var(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER)+"/5"+mstr(108));
         floater(self_obj, 101+global_var(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER), COLOR_MSG_GREEN);
         item_remove(dude_obj, objUsedPid, 1)
         inc_global_var(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER);
         set_lvar_bit_on(LVAR_Self_Flags, bit_20);
         end
         else begin
         display_mstr(109);
         end
      end
      end
   end
end

procedure combat_p_proc begin
   effectAttak(0,2,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (cur_map_index == MAP_DAYGLOW_BONITA_DUM) then begin
      if (getQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER) < 10) then begin
      setQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER, qStop);
      end
   end
   if (source_obj == dude_obj) then begin
      if (cur_map_index == MAP_DAYGLOW_CAMP_SAN_ISIDRO) then begin
      set_global_var(GVAR_DAYGLOW_PASS, -1);
      end
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_DAYGLOW_CORONADO_STATUS) > 0) then begin
      floater(self_obj, random(130,134), COLOR_MSG_GREEN);
   end
   else if (global_var(GVAR_DAYGLOW_CORONADO_STATUS) < 0) then begin
      floater(self_obj, random(135,139), COLOR_MSG_NORMAL);
   end

   else if DudePerkHas(PERK_ghouling_fiziology) then begin
      floater(self_obj, random(120,129), COLOR_MSG_NORMAL);
   end
   else if (random(0,100) >= 50) then begin
      floater(self_obj, random(120,129), COLOR_MSG_NORMAL);
   end
   else begin
      if town_rep_is_liked then begin
      floater(self_obj, random(115,119), COLOR_MSG_NORMAL);
      end
      else begin
      floater(self_obj, random(110,114), COLOR_MSG_NORMAL);
      end
   end
end
