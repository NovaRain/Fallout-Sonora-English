#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Супермутант-телохранитель ДеСанти

#define NAME                            SCRIPT_YCSantMt
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_INFIERNO_CULT    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SUPERMUTANT        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog101;
procedure NodeDialog102;
procedure NodeDialog103;
procedure NodeDialog104;
procedure NodeDialog105;

//---------------------

#define LVAR_Food_Amnt                     (5) // число использованной еды

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);
       if ((getQuest(GVAR_DAYGLOW_QST_SUPERMUT) == 1) and (cur_map_index != MAP_DAYGLOW_PIRS)) then begin
       set_obj_visibility(self_obj, 0);
       end
       if ((getQuest(GVAR_DAYGLOW_QST_SUPERMUT) == 10) and (cur_map_index == MAP_DAYGLOW_PIRS)) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_DAYGLOW_GHOUL);
       set_obj_visibility(self_obj, 0);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 3) then begin
      inc_local_var(LVAR_Food_Amnt);
      if (local_var(LVAR_Food_Amnt)>=3) then begin
      floater(self_obj, 106, COLOR_MSG_GREEN);
      bitDialogExp(bit_2,0)
      end
      else begin
      floater(self_obj, 105, COLOR_MSG_NORMAL);
      end
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,5,5,5) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   GomerAttakDude
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_DAYGLOW_QST_SUPERMUT) > -10) then begin
   setQuest(GVAR_DAYGLOW_QST_SUPERMUT, -1);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(-1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)

   if (cur_map_index == MAP_DAYGLOW_PIRS) then begin
   floater(self_obj, 103, COLOR_MSG_NORMAL);
   end
   else if (getQuest(GVAR_DAYGLOW_QST_SUPERMUT) == qStart) then begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else begin
   floater(self_obj, 102, COLOR_MSG_RED);
   end
end

procedure Node998 begin
   GomerAttakDude
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_2) != 0) then begin
      call NodeDialog104;
   end
   else if (bitDialog(bit_1) == 0) then begin
      call NodeDialog101;
   end
   else begin
      call NodeDialog103;
   end
end

procedure NodeDialog101 begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,0,NodeDialog102)
end

procedure NodeDialog102 begin
   Reply(202)
   NAnswer(203,0,NodeDialog103)
end

procedure NodeDialog103 begin
   Reply(204)
   NAnswer(205,0,Node999)
end

procedure NodeDialog104 begin
   Reply(206)
   NAnswer(207,0,NodeDialog105)
end

procedure NodeDialog105 begin
   setQuest(GVAR_DAYGLOW_QST_SUPERMUT, 2);
   if (partyPidMtn!=0) then begin
   floater(partyPidMtn, 103, COLOR_MSG_NORMAL);
   end
   Reply(208)
   NAnswer(209,0,Node999)
end
