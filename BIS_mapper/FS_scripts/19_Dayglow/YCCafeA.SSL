#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCCafeA
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW
#define BOX_TILE                        10  // номер тайла с личным товарным ящиком (-1, если не надо; не использовать значения <10; не может совпадать с чужими ящиками на одной локации)

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure setBarterA;
procedure setBarterB;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog101;
procedure NodeFstDialog102;
procedure NodeFstDialog103;
procedure NodeFstDialog104;
procedure NodeFstDialog105;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog3;
procedure NodeBarter;
procedure NodeBarterMany;
procedure NodeBarterMany1;
procedure NodeBarterMany2;
procedure NodeBarterMany3;
procedure NodeBarterMany4;
procedure NodeBarterMany5;
procedure NodeBarterMany999;
procedure NodeBarterManyPrev;
procedure NodeBarterManyNo;
procedure NodeBarterManyOk;
procedure NodeBarterCoffe;
procedure NodeBarterCoffe2;
procedure NodeBarterCoffe3;
procedure NodeBarterCoffe4;
procedure NodeBonus;
procedure NodeBarterQst1;
procedure NodeBarterQst2;
procedure NodeBarterQst3;
procedure NodeBarterQst4;
procedure NodeBarterQst5;
procedure NodeBarterQst6;
procedure NodeBarterQst7;
procedure NodeBarterQst8;

//---------------------

//#define LVAR_Flag_A                     (8) // отвечает за ...

import variable box_stop_use;

variable manyCoast;
variable manyAmnt;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_BarterBoxProc(ONE_GAME_WEEK)
   end
end

procedure setBarterA begin
        check_restock_item(PID_M_RADAWAY, 3, 3, 100)
        check_restock_item(PID_M_RAD_X, 3, 3, 100)
        check_restock_item(PID_M_ANTIDOTE, 5, 5, 100)
        check_restock_item(PID_M_STIMPAK, 8, 8, 100)

        check_restock_item(PID_M_FRUIT, 2, 2, 100)
        check_restock_item(PID_M_NUKA_COLA, 5, 7, 100)
        check_restock_item(PID_M_MEX_BEER, 5, 7, 100)

        //- крышки
        check_restock_many(300,400)
end

procedure setBarterB begin
   //! пока пусто
   set_BarterDopusk;
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if ((objUsedPid == PID_Q_DAYGLOW_BORDERBADGE_A) or (objUsedPid == PID_Q_DAYGLOW_BORDERBADGE_B)) then begin
       script_overrides;
       floater(self_obj, 102, COLOR_MSG_NORMAL);
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_DAYGLOW_CONTRACT_VISKI) < qEnd) then begin
   setQuest(GVAR_DAYGLOW_CONTRACT_VISKI, qStop);
   end
   if (getQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER) < qEnd) then begin
   setQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER, qStop);
   end
   kill_critter_opt
   kill_critter_box
end

procedure talk_p_proc begin
   setReaction(1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,75) // поправка к отношению за принадлежность к фракции

   if (get_REP <= -15) then begin
   z_floater(self_obj, 235, COLOR_MSG_RED);
   end
   else begin
   dialog_proc_barter(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   /*else if ((bitDialog(bit_2) == 0) and (getQuest(GVAR_DAYGLOW_MAIN_A) >= qEnd)) then begin
   call NodeSndDialog2;
   end*/
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   incBarterMod(50);

   if DudePerkHas(PERK_ghouling_fiziology) then begin
   Reply(216)
   NAnswer(mstr(217)+dude_name+mstr(218),1,NodeFstDialog104)
   zBAnswer(219,0,NodeFstDialog105)
   end
   else begin
   incBarterMod(50);
   Reply(200)
   NAnswer(201,0,NodeFstDialog101)
   NAnswer(202,0,Node999)
   end
end

procedure NodeFstDialog101 begin
   Reply(203)
   NAnswer(204,0,NodeFstDialog102)
   BAnswer(205,0,NodeFstDialog103)
end

procedure NodeFstDialog102 begin
   setBonusReaction(10)
   Reply(206)
   NAnswer(207,0,NodeFstDialog104)
end

procedure NodeFstDialog103 begin
   setBonusReaction(-10)
   set_REP(-1)
   Reply(206)
   NAnswer(207,0,NodeFstDialog104)
end

procedure NodeFstDialog104 begin
   zReply(208)
   NAnswer(209,0,NodeBarter)
end

procedure NodeFstDialog105 begin
   setBonusReaction(-10)
   set_REP(-1)
   call NodeFstDialog104;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (get_REP <= -5) then begin
   Reply(236)
   end
   else if (DudePerkHas(PERK_ghouling_fiziology) or (get_REP >= 25)) then begin
   zReply(212)
   end
   else begin
   Reply(213)
   end

   NAnswer(215,0,NodeBarter)
   if (item_amnt(dude_obj,PID_MANY) > 0) then begin
   NAnswer(260,4,NodeBarterMany)
   NAnswer(276,-3,NodeBarterMany)
   end

   if (getQuest(GVAR_DAYGLOW_CONTRACT_COFFE) >= qStart) then begin
      if (item_amnt(dude_obj,PID_COFFEE)>0) then begin
      GAnswer(231,0,NodeBarterCoffe3)
      end
   end
   if (getQuest(GVAR_DAYGLOW_CONTRACT_COFFE) == qZero) then begin
      NAnswer(223,3,NodeBarterCoffe)
   end

   if (getQuest(GVAR_DAYGLOW_RADIO) == 2) then begin
      if (bitDialog(bit_3) == 0) then begin
      NAnswer(250,0,NodeBonus)
      end
   end

   if (getQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER) == qZero) then begin
      if (getQuest(GVAR_DAYGLOW_MAIN_A) >= qEnd) then begin
      NAnswer(280,3,NodeBarterQst1)
      end
   end
   if (getQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER) == (qStart+5)) then begin
      NAnswer(286,0,NodeBarterQst3)
   end

   if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_27) != 0) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_FILM) == 0) then begin
      NAnswer(292,0,NodeBarterQst4)
      end
   end

   if (getQuest(GVAR_DAYGLOW_QST_BUTLEGER) == qEnd) then begin
      if (item_amnt(dude_obj,PID_Q_DAYGLOW_FOODPROCESSOR)>0) then begin
      NAnswer(295,0,NodeBarterQst5)
      end
   end

   if (global_var(GVAR_DAYGLOW_QST_GOMER) == 1) then begin
      //Я ищу отряд супермутантов. Тебе известно о таком?
   end

   NAnswer(214,0,Node999)
end

procedure NodeSndDialog2 begin
   bitDialogExp(bit_2,0)

   if DudePerkHas(PERK_ghouling_fiziology) then begin
      item_add(dude_obj,PID_M_MEX_BEER,3)
      item_add(dude_obj,PID_M_TV_DINNER,1)
      decBarterMod(25);
      Reply(mstr(247)+dude_name+mstr(248))
      NAnswer(245,0,NodeBarter)
      NAnswer(246,0,NodeSndDialog)
   end
   else begin
      item_add(dude_obj,PID_M_RADAWAY,1)
      item_add(dude_obj,PID_M_RAD_X,1)
      decBarterMod(35);
      Reply(mstr(240)+dude_name+mstr(241))
      NAnswer(242,0,NodeSndDialog3)
      NAnswer(243,0,NodeSndDialog3)
   end
end

procedure NodeSndDialog3 begin
   Reply(244)
   NAnswer(245,0,NodeBarter)
   NAnswer(246,0,NodeSndDialog)
end

procedure NodeBarter begin
   goBarterMod;
   Reply(210)
   NAnswer(220,0,NodeStartDialog)
   NAnswer(211,0,Node999)
end

//--- ДЕНЬГИ НА КРЫШКИ

procedure NodeBarterMany begin
   manyAmnt := 0;
   manyCoast := 10;
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   manyCoast := 16;
   end
   if town_rep_is_antipaty then begin
   manyCoast -= 2;
   end
   if town_rep_is_hated then begin
   manyCoast := manyCoast/2;
   end

   if (getQuest(GVAR_DAYGLOW_MAIN_D) == 100) then begin
   manyCoast -= manyCoast/2;
   end
   else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 101) then begin
   manyCoast *= 2;
   end
   else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 104) then begin
   manyCoast -= manyCoast/2;
   end

   if (manyCoast <= 0) then begin
   manyCoast := 1;
   end

   Reply(mstr(261)+manyCoast+mstr(262))

   NAnswer(265,0,NodeBarterMany1)
   if (item_amnt(dude_obj,PID_MANY) >= 2) then begin
   NAnswer(266,0,NodeBarterMany2)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 3) then begin
   NAnswer(267,0,NodeBarterMany3)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 4) then begin
   NAnswer(268,0,NodeBarterMany4)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 5) then begin
   NAnswer(269,0,NodeBarterMany5)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 10) then begin
   //NAnswer(270,0,NodeBarterMany10)
   end
   NAnswer(271,0,NodeBarterMany999)

   NAnswer(264,4,NodeBarter)
   NAnswer(263,4,Node999)
   NAnswer(277,-3,Node999)
end

procedure NodeBarterMany1 begin
   manyAmnt := 1;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany2 begin
   manyAmnt := 2;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany3 begin
   manyAmnt := 3;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany4 begin
   manyAmnt := 4;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany5 begin
   manyAmnt := 5;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany999 begin
   manyAmnt := item_amnt(dude_obj,PID_MANY);
   call NodeBarterManyPrev;
end

procedure NodeBarterManyPrev begin
   if (manyAmnt <= 0) then begin
   call NodeBarterManyNo;
   end
   else if (item_amnt(dude_obj,PID_MANY) >= manyAmnt) then begin
   call NodeBarterManyOk;
   end
   else begin
   call NodeBarterManyNo;
   end
end

procedure NodeBarterManyNo begin
   Reply(272)
   NAnswer(273,0,Node999)
end

procedure NodeBarterManyOk begin
   item_add(dude_obj,PID_BOOTLE_CAPS,manyAmnt*manyCoast)
   item_remove(dude_obj,PID_MANY,manyAmnt)
   Reply(274)
   NAnswer(275,0,NodeSndDialog)
end

//--- РАССПРОСЫ
/*
procedure NodeAnswer begin
   Reply(221)
   if (getQuest(GVAR_DAYGLOW_CONTRACT_COFFE) == qZero) then begin
   NAnswer(223,0,NodeBarterCoffe)
   end
   NAnswer(222,0,Node999)
end
*/

procedure NodeBarterCoffe begin
   Reply(224)
   GAnswer(225,0,NodeBarterCoffe2)
   BAnswer(226,0,NodeBarter)
end

procedure NodeBarterCoffe2 begin
   setBonusReaction(5)
   setQuest(GVAR_DAYGLOW_CONTRACT_COFFE, qStart);
   Reply(227)
   if (item_amnt(dude_obj,PID_COFFEE)>0) then begin
   GAnswer(230,0,NodeBarterCoffe3)
   end
   NAnswer(228,0,Node999)
   NAnswer(229,0,Node999)
end

procedure NodeBarterCoffe3 begin
   if (item_amnt(dude_obj,PID_COFFEE)>0) then begin
   call NodeBarterCoffe4;
   end
   else begin
   call NodeSndDialog;
   end
end

procedure NodeBarterCoffe4 begin
   inc_global_var(GVAR_DAYGLOW_CONTRACT_COFFE);
   exp_add(200);
   item_remove(dude_obj,PID_COFFEE,1)
   item_add(dude_obj, PID_BOOTLE_CAPS, (proto_data(PID_COFFEE, it_cost)*3))
   Reply(mstr(232)+(proto_data(PID_COFFEE, it_cost)*3)+mstr(233))
   NAnswer(234,0,Node999)
end

procedure NodeBonus begin
   bitDialogExp(bit_3,0)
   item_add(dude_obj, PID_M_RAD_X, 1)
   item_add(dude_obj, PID_M_RADAWAY, 1)
   item_add(dude_obj, PID_M_ANTIDOTE, 1)
   item_add(dude_obj, PID_M_STIMPAK, 1)

   zReply(251)
   NAnswer(252,0,NodeBarter)
   NAnswer(253,0,Node999)
end

procedure NodeBarterQst1 begin
   Reply(281)
   NAnswer(283,0,NodeBarterQst2)
   NAnswer(282,0,NodeSndDialog)
end

procedure NodeBarterQst2 begin
   setQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER, qStart);
   set_map_music(MAP_DAYGLOW_BONITA_DUM, "fs_daygl");
   item_add(dude_obj,PID_M_TV_DINNER,5)
   Reply(284)
   NAnswer(285,0,Node999)
end

procedure NodeBarterQst3 begin
   setQuest(GVAR_DAYGLOW_CONTRACT_CAFE_CURIER, 10);
   set_REP(3)
   exp_add(500);
   item_add(dude_obj,PID_BOOTLE_CAPS,20)

   if (get_REP>=10) then begin
   item_add(dude_obj,PID_M_TV_DINNER,1)
   Reply(289)
   NAnswer(290,0,Node999)
   BAnswer(291,0,Node999)
   end
   else begin
   Reply(287)
   NAnswer(288,0,Node999)
   end
end

procedure NodeBarterQst4 begin
   Reply(293)
   NAnswer(294,0,NodeSndDialog)
end

procedure NodeBarterQst5 begin
   Reply(296)
   NAnswer(297,0,NodeBarterQst6)
   NAnswer(301,0,NodeBarterQst7)
end

procedure NodeBarterQst6 begin
   Reply(298)
   NAnswer(299,0,Node999)
   NAnswer(300,0,Node999)
end

procedure NodeBarterQst7 begin
   if (item_amnt(dude_obj,PID_Q_DAYGLOW_FOODPROCESSOR)>0) then begin
      call NodeBarterQst8;
   end
   else begin
      call NodeBarterQst5;
   end
end

procedure NodeBarterQst8 begin
   item_remove(dude_obj,PID_Q_DAYGLOW_FOODPROCESSOR,1)
   setQuest(GVAR_DAYGLOW_QST_BUTLEGER, 20);
   item_add(dude_obj, PID_BOOTLE_CAPS, 200)
   exp_add(500);
   set_REP(3)

   if DudePerkHas(PERK_ghouling_fiziology) then begin
   Reply(305)
   end
   else begin
   Reply(302)
   end
   NAnswer(303,0,Node999)
   NAnswer(304,0,Node999)
end
