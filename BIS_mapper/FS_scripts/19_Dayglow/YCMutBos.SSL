#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Паладин БС

#define NAME                            SCRIPT_YCMutBos
#define TOWN_REP_VAR                    -1  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_ROBOTS // TEAM_BROTHERS_STEEL   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_BOS_PALADIN        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog;
procedure NodeDialog1;
procedure NodeDialog2;
procedure NodeDialog3;
procedure NodeDialog4;
procedure NodeDialog5;
procedure NodeDialog6;
procedure NodeDialog6b;
procedure NodeDialog7;
procedure NodeDialog8;

//---------------------

#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

import variable obj_critter_a; // указатель на криттера 1

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);
       if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) == 0) then begin
       if (getQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT) == qStart) then begin
       set_obj_visibility(self_obj, 0);
       end
       end

       if (item_amnt(self_obj,PID_TEST) > 0) then begin
           if (item_amnt(self_obj,PID_TEST) == 2) then begin
           set_local_var(LVAR_Tile_B,tile_num_in_direction(HomeTile,0,11));
           end
           item_remove(self_obj,PID_TEST,999)
       end
   end
end

procedure map_update_p_proc begin
   if ((global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) >= 2) or (getQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT) <= -100)) then begin
       set_obj_visibility(self_obj, 1);
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin

       if ((map_var(MVAR_DIEGOPTRL_SIGNAL_ACTIV)>0) and (HomeTile == 24106) and (combat_is_initialized == false)) then begin
         timer_move_to_tile(0,map_var(MVAR_DIEGOPTRL_SIGNAL_ACTIV),5)

         if (self_tile == map_var(MVAR_DIEGOPTRL_SIGNAL_ACTIV)) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
            reg_anim_animate(self_obj,ANIM_magic_hands_middle,-1);
            reg_anim_end();

            if (random(0,5+dude_luck) == 0) then begin
            floater(self_obj, random(130,133), COLOR_MSG_NORMAL);
            set_map_var(MVAR_DIEGOPTRL_SIGNAL_ACTIV,0);
            end
            else begin
            floater(self_obj, random(120,125), COLOR_MSG_RED);
            end
         end
         else begin
            floater(self_obj, random(110,115), COLOR_MSG_RED);
         end

       end
       else if (local_var(LVAR_Tile_B) > 0) then begin
         timer_move_patrol(HomeTile,local_var(LVAR_Tile_B))
       end
       else begin
         timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   if (map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG)>=0) then begin
      if (obj_can_see_obj(self_obj, dude_obj) == true) then begin
      dialogue_system_forcing(10)
      end
   end
   if (obj_critter_a != 0) then begin
      if (obj_can_see_obj(self_obj, obj_critter_a) == true) then begin
         if dude_is_sneaking then begin
            if (tile_distance_objs(self_obj,obj_critter_a) <= self_perception) then begin
            attack(obj_critter_a);
            end
         end
         else begin
            attack(obj_critter_a);
         end
      end
   end
   else if (partyPidMtn != 0) then begin
      if (obj_can_see_obj(self_obj, partyPidMtn) == true) then begin
         if dude_is_sneaking then begin
            if (tile_distance_objs(self_obj,partyPidMtn) <= self_perception) then begin
            attack(partyPidMtn);
            end
         end
         else begin
            attack(partyPidMtn);
         end
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,5,5,5) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   set_map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG,3);
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
   set_map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG,3);
   end
   kill_critter_opt
end

procedure talk_p_proc begin

   //Added the test for nightkin cap, to prevent being teleported out of base with the captain - cambragol
   if (global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP) == 1) then begin
         z_floater(self_obj, 226, COLOR_MSG_RED);
         call Node998;
   end
   else if (map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG)>=0) then begin
   setReaction(0) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else begin
   z_floater(self_obj, 102, COLOR_MSG_NORMAL);
   end
end

procedure Node998 begin
   set_map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG,4);
   setNodeCombat;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG) >= 3) then begin
   call NodeDialog1;
   end
   else begin
   call NodeDialog;
   end
end

procedure NodeDialog begin

      if (map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG) == 2) then begin
      Reply(213)
      end
      else if (map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG) == 1) then begin
      Reply(212)
      end
      else begin
         Reply(200)
      end

      if (global_var(GVAR_TITUL_BOS_SCRIBE)>0) then begin
      GAnswer(mstr(203)+dude_name+mstr(207),0,NodeDialog3)
      end
      if (global_var(GVAR_TITUL_BOS_CAVALER)>0) then begin
      GAnswer(mstr(204)+dude_name+mstr(207),0,NodeDialog3)
      end
      if (global_var(GVAR_TITUL_BOS_PALADIN)>0) then begin
      GAnswer(mstr(205)+dude_name+mstr(207),0,NodeDialog3)
      end
      if (global_var(GVAR_TITUL_BOS_SCOUT)>0) then begin
      GAnswer(mstr(205)+dude_name+mstr(207),0,NodeDialog3)
      end

      if (getQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT) >= qStart) then begin
      NAnswer(217,0,NodeDialog5)
      end

      BAnswer(202,0,Node998)
      NAnswer(201,0,NodeDialog2)
   
end

procedure NodeDialog1 begin
   Reply(214)
   BAnswer(215,0,Node998)
   BAnswer(216,0,Node998)
end

procedure NodeDialog2 begin
   gfade_out(ONE_GAME_SECOND);
   inc_map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG);
   move_to(dude_obj, 22076, 1);
   anim(dude_obj, ANIMATE_ROTATION, 4);
   gfade_in(ONE_GAME_SECOND);
   call Node999;
end

procedure NodeDialog3 begin
   set_map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG,-1);
   zReply(208)
   NAnswer(209,0,NodeDialog4)
end

procedure NodeDialog4 begin
   Reply(210)
   NAnswer(211,0,Node999)
end

procedure NodeDialog5 begin
   Reply(218)
   NAnswer(222,0,NodeDialog7)
   GAnswer(219,0,NodeDialog6)
end

procedure NodeDialog6 begin
   Reply(220)
   NAnswer(221,0,NodeDialog6b)
end

procedure NodeDialog6b begin
   gfade_out(ONE_GAME_SECOND);
   setQuest(GVAR_DAYGLOW_QST_GOMERSUPERMUT, -100);
   set_global_var(GVAR_DAYGLOW_STATUS_GOMER_CAP,-1);
   inc_global_var_amt(GVAR_TOWN_REP_PUERTO, 3);
   set_map_var(MVAR_DIEGOPTRL_PALADIN_STOP_DIALOG,-1);
   move_to(dude_obj, 22076, 1);
   anim(dude_obj, ANIMATE_ROTATION, 4);
   exp_add(1000);
   gfade_in(ONE_GAME_SECOND);
   call Node999;
end

procedure NodeDialog7 begin
   Reply(223)
   BAnswer(224,0,Node998)
   GAnswer(219,0,NodeDialog8)
end

procedure NodeDialog8 begin
   Reply(225)
   NAnswer(221,0,NodeDialog6b)
end
