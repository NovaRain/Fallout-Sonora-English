#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_VCCrzCtz
#define TOWN_REP_VAR                    GVAR_TOWN_REP_VILLA   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_VILLA_PEASAN     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_TRIBLE_VILLA       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      40 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeQst100;
procedure NodeQst101;
procedure NodeQst102;
procedure NodeQst103;
procedure NodeQst200;
procedure NodeQst201;
procedure NodeQst300;
procedure NodeQst301;
procedure NodeQst302;
procedure NodeQst303;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if ((getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qEnd) or (getQuest(GVAR_VILLA_QST_CHRIS) >= qEnd)) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
       else begin
       set_obj_visibility(self_obj, 0);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       if ((global_var(GVAR_VILLA_STATUS_CHRIS) == 0) or (global_var(GVAR_VILLA_STATUS_CHRIS) == 1)) then begin
       timer_float_reply(110,115)
       end
   end
end

procedure critter_p_proc begin
   set_critter_options(3,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_VILLA_QST_CHRIS) == qStart) then begin
   setQuest(GVAR_VILLA_QST_CHRIS,qZero);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,1,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (getQuest(GVAR_VILLA_QST_CHRIS) == qStart) then begin
      if (global_var(GVAR_VILLA_STATUS_CHRIS) == -2) then begin
      call NodeQst300;
      end
      else if (global_var(GVAR_VILLA_STATUS_CHRIS) == 2) then begin
      call NodeQst302;
      end
      else begin
      call NodeQst200;
      end
   end
   else begin
      call NodeQst100;
   end
end

procedure NodeQst100 begin
   zReply(200)
   if (global_var(GVAR_VILLA_STATUS_CHRIS) == 0) then begin
   NAnswer(201,1,NodeQst101)
   end
   else begin
   NAnswer(201,1,Node999)
   end
end

procedure NodeQst101 begin
   Reply(202)
   NAnswer(203,1,NodeQst102)
end

procedure NodeQst102 begin
   Reply(204)
   NAnswer(205,1,NodeQst103)
end

procedure NodeQst103 begin
   set_global_var(GVAR_VILLA_STATUS_CHRIS,1);
   setQuest(GVAR_VILLA_QST_CHRIS,qStart);
   Reply(206)
   BAnswer(207,1,Node999)
   NAnswer(208,1,Node999)
end

procedure NodeQst200 begin
   Reply(210)
   if (global_var(GVAR_VILLA_STATUS_CHRIS) < 0) then begin
   NAnswer(220,1,NodeQst300)
   end
   NAnswer(212,1,NodeQst201)
   NAnswer(211,1,Node999)
end

procedure NodeQst201 begin
   Reply(213)
   NAnswer(214,1,Node999)
end

procedure NodeQst300 begin
   setQuest(GVAR_VILLA_QST_CHRIS,qEnd);
   set_REP(5)
   exp_add(350);
   if (global_var(GVAR_VILLA_STATUS_CHRIS) == -2) then begin
   Reply(223)
   NAnswer(224,1,NodeQst301)
   end
   else begin
   Reply(221)
   NAnswer(222,1,Node999)
   end
end

procedure NodeQst301 begin
   item_add(dude_obj,PID_AMMO_SHOTGUN_SHELLS,random(3,5))

   if (random(0,1) == 0) then begin
   item_add(dude_obj,PID_AMMO_308,random(3,6))
   end
   else begin
   item_add(dude_obj,PID_AMMO_5_MM,random(2,3)) // в пачке 5 патронов
   end

   if (random(0,1) == 0) then begin
   item_add(dude_obj,PID_AMMO_10_MM,random(4,8))
   end
   else begin
   item_add(dude_obj,PID_AMMO_44_MAGNUM,random(4,8))
   end

   Reply(225)
   NAnswer(226,1,Node999)
end

procedure NodeQst302 begin
   Reply(227)
   NAnswer(228,1,NodeQst303)
end

procedure NodeQst303 begin
   setQuest(GVAR_VILLA_QST_CHRIS,qEnd);
   exp_add(350);
   Reply(229)
   NAnswer(230,1,Node999)
end
